[[model]]
name: Takeuchi 2013 T-type calcium channel
author: Charles Houston
desc: """
      The Takeuchi 2013 model for the T-type calcium channel in HL-1 myocytes.
      Reversal potential adjusted for data from Deng et al., 2009.
      """
ref: """
     A Takeuchi et al. (2013) The mitochondrial Na+-Ca2+ exchanger, NCLX, regulates
     automaticity of HL-1 cardiomyocytes.
     """
# Initial values
membrane.V = -67
icat.d = 0.01
icat.f = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -icat.i_CaT
    in [mV]

[icat]
use membrane.V as V
g_CaT = 0.4122
    in [mS/uF]
E_CaT = 45 # Dias et al., 2014 report 33mV for HL1-6
    in [mV]
G_CaT = g_CaT * d * f
    in [mS/uF]
i_CaT = G_CaT * (V - E_CaT)
    in [pA/pF]

dot(d) = (dss - d)/tau_d
    in [1]
tau_d = (k_dtau1*exp((V+k_dtau2)/k_dtau3) + k_dtau4*exp(-(V+k_dtau5)/k_dtau6))^(-1)
    in [1]
dss = (1 + exp(-(V+k_dss1)/k_dss2))^(-1)
    in [1]

k_dss1 = 30
    in [mV]
k_dss2 = 6.0
    in [mV]
k_dtau1 = 1.068
    in [1]
k_dtau2 = 26.3
    in [mV]
k_dtau3 = 30
    in [mV]
k_dtau4 = 1.068
    in [1]
k_dtau5 = 26.3
    in [mV]
k_dtau6 = 30
    in [mV]

dot(f) = (fss - f)/tau_f
    in [1]
fss = (1 + exp((V+k_fss1)/k_fss2))^(-1)
    in [1]
tau_f = (k_ftau1 * 1e-2 * exp(-(V+k_ftau2)/k_ftau3) + k_ftau4 * 1e-2 * exp((V+k_ftau5)/k_ftau6))^(-1)
    in [1]

k_fss1 = 48
    in [mV]
k_fss2 = 7.0
    in [mV]
k_ftau1 = 1.53
     in [1]
k_ftau2 = 61.7
     in [mV]
k_ftau3 = 83.3
     in [mV]
k_ftau4 = 1.5
     in [1]
k_ftau5 = 61.7
     in [mV]
k_ftau6 = 30
     in [mV]
