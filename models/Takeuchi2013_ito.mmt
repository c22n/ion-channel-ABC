[[model]]
name: Takeuchi 2013 transient outward K Current
author: Charles Houston

# Initial values
membrane.V = -67.0
ito.xto = 4.05157399999999963e-3
ito.yto = 0.9945511

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ito.i_to
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
Ko = 5400
    in [uM]
Ki = 143720
    in [uM]

[ito]
g_to = 0.12375 [nS/pF]
     in [nS/pF]

xto_ss = 1 / (1 + exp(-(membrane.V - xto_ssk1) / xto_ssk2))
    in [1]
tau_xto = tau_xtok1 + tau_xtok2 * exp(-((membrane.V) / tau_xtok3) ^ 2)
    in [ms]
yto_ss = 1 / (1 + exp((membrane.V + yto_ssk1) / yto_ssk2))
    in [1]
tau_yto = tau_ytok1 + tau_ytok2 * exp(-((membrane.V + tau_ytok3) / tau_ytok4) ^ 2)
    in [ms]
dot(xto) = (xto_ss - xto) / tau_xto
    in [1]
dot(yto) = (yto_ss - yto) / tau_yto
    in [1]
xto_ssk1 = 1 [mV]
    in [mV]
xto_ssk2 = 11 [mV]
    in [mV]
tau_xtok1 = 1.5 [ms]
    in [ms]
tau_xtok2 = 3.5 [ms]
    in [ms]
tau_xtok3 = 30 [mV]
    in [mV]
yto_ssk1 = 40.5 [mV]
    in [mV]
yto_ssk2 = 11.5 [mV]
    in [mV]
tau_ytok1 = 21.21 [ms]
    in [ms]
tau_ytok2 = 38.4525 [ms]
    in [ms]
tau_ytok3 = 52.45 [mV]
    in [mV]
tau_ytok4 = 15.8827 [mV]
    in [mV]

E_K = membrane.R * membrane.T / membrane.F * log(membrane.Ko / membrane.Ki)
    in [mV]
G_to = g_to * xto * yto
    in [nS/pF]
i_to = G_to * (membrane.V - E_K)
    in [pA/pF]
