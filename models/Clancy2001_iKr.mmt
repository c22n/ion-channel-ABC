[[model]]
name: Clancy 2001 Rapidly activating delayed rectifier K Current
author: Charles Houston

# Initial values
membrane.V = -82.4202
ikr.P_C3                                = 0.96
ikr.P_C2                                = 0.01
ikr.P_C1                                = 0.01
ikr.P_O                                 = 0.01
ikr.P_I                                 = 0.01

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ikr.i_Kr
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
Ko = 5400
    in [uM]
Ki = 143720
    in [uM]
Nao = 140000
    in [uM]
Nai = 14237.1
    in [uM]

[ikr]
dot(P_C1) = -(beta_in + alpha_alpha + alpha_alpha) * P_C1 + alpha_in * P_C2 + beta_beta * P_O + mu * P_I
    in [1]
dot(P_C2) = -(beta + alpha_in) * P_C2 + alpha * P_C3 + beta_in * P_C1
    in [1]
dot(P_C3) = beta * P_C2 - alpha * P_C3
    in [1]
dot(P_I) = -(mu + alpha_i) * P_I + alpha_alpha * P_C1 + beta_i * P_O
    in [1]
dot(P_O) = -(beta_beta + beta_i) * P_O + alpha_alpha * P_C1 + alpha_i * P_I
    in [1]

alpha = alphak1 * exp(alphak2 * (membrane.V - alphak3))
    in [mS/mF]
alpha_alpha = alpha_alphak1 * exp(alpha_alphak2 * (membrane.V - alpha_alphak3))
    in [mS/mF]
alpha_i = alpha_ik1 * exp(-alpha_ik2 * (membrane.V + alpha_ik3)) * alpha_ik4 / (membrane.Ko/1000)
    in [mS/mF]
beta = betak1 * exp(-betak2 * membrane.V)
    in [mS/mF]
beta_beta = beta_betak1 * exp(-beta_betak2 * membrane.V)
    in [mS/mF]
beta_i = beta_ik1 * exp(beta_ik2 * membrane.V) * (beta_ik3 / (membrane.Ko/1000)) ^ 0.3
    in [mS/mF]
mu = alpha_i * beta_beta * alpha_alpha / (alpha_alpha * beta_i)
    in [mS/mF]

# Parameters
alphak1 = 55.5 [mS/mF]
    in [mS/mF]
alphak2 = 5.54715299999999981e-2
    in [1]
alphak3 = 12 [mV]
    in [mV]
alpha_alphak1 = 65.5 [mS/mF]
    in [mS/mF]
alpha_alphak2 = 5.54715299999999981e-2
    in [1]
alpha_alphak3 = 36 [mV]
    in [mV]
alpha_ik1 = 439 [mS/mF]
    in [mS/mF]
alpha_ik2 = 0.02352
    in [1]
alpha_ik3 = 25 [mV]
    in [mV]
alpha_ik4 = 4.5 [uM]
    in [uM]
alpha_in = 2172
    in [mS/mF]
betak1 = 2.357 [mS/mF]
    in [mS/mF]
betak2 = 0.036588
    in [1]
beta_betak1 = 2.9357 [mS/mF]
    in [mS/mF]
beta_betak2 = 0.02158
    in [1]
beta_ik1 = 656 [mS/mF]
    in [mS/mF]
beta_ik2 = 0.000942
    in [1]
beta_ik3 = 4.5 [uM]
    in [uM]
beta_in = 1077
    in [mS/mF]

E_K = membrane.R * membrane.T / membrane.F * log((0.98 * membrane.Ko + 0.02 * membrane.Nao) / (0.98 * membrane.Ki + 0.02 * membrane.Nai))
    in [mV]
g_Kr_max = 0.0135 [mS/mF]
    in [mS/mF]
g_Kr = g_Kr_max * (membrane.Ko/1000) ^ 0.59
    in [mS/mF]
G_Kr = g_Kr * P_O
    in [mS/mF]
i_Kr = g_Kr * P_O * (membrane.V - E_K)
    in [pA/pF]
