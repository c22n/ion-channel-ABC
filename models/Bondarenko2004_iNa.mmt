[[model]]
name: Bondarenko 2004 Fast Sodium Channel
author: Charles Houston
desc: The Bondarenko 2004 model for the fast sodium channel in HL-1 myocytes.
ref: """
Bondarenko et al. (2004)
"""
# Initial values
membrane.V = -67.0
ina.C_Na2 = 0.020752
ina.C_Na1 = 2.79132000000000024e-4
ina.O_Na = 7.13482999999999984e-7
ina.IF_Na = 1.53176000000000012e-4
ina.I1_Na = 6.73345000000000052e-7
ina.I2_Na = 1.55786999999999992e-9
ina.IC_Na2 = 0.0113879
ina.IC_Na3 = 0.34278

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ina.i_Na
    in [mV]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
T = 298
    in [K]
Nao = 140000
    in [uM]
Nai = 14237.1
    in [uM]

[ina]
use membrane.V as V
g_Na = 13
    in [mS/uF]
E_Na = 58.686 # calculated from Nernst potential, 23.2 is value from Dias 2014
    in [mV]
# E_Na = membrane.R * membrane.T / membrane.F * log(membrane.Nao / membrane.Nai)
#     in [mV]
G_Na = g_Na * O_Na
    in [mS/uF]
i_Na = G_Na * (V - E_Na)
    in [pA/pF]

dot(C_Na1) = alpha_Na12 * C_Na2 + beta_Na13 * O_Na + alpha_Na3 * IF_Na - (beta_Na12 * C_Na1 + alpha_Na13 * C_Na1 + beta_Na3 * C_Na1)
    in [1]
dot(C_Na2) = alpha_Na11 * C_Na3 + beta_Na12 * C_Na1 + alpha_Na3 * IC_Na2 - (beta_Na11 * C_Na2 + alpha_Na12 * C_Na2 + beta_Na3 * C_Na2)
    in [1]
C_Na3 = 1 - (O_Na + C_Na1 + C_Na2 + IF_Na + I1_Na + I2_Na + IC_Na2 + IC_Na3)
    in [1]
dot(I1_Na) = alpha_Na4 * IF_Na + beta_Na5 * I2_Na - (beta_Na4 * I1_Na + alpha_Na5 * I1_Na)
    in [1]
dot(I2_Na) = alpha_Na5 * I1_Na - beta_Na5 * I2_Na
    in [1]
dot(IC_Na2) = alpha_Na11 * IC_Na3 + beta_Na12 * IF_Na + beta_Na3 * C_Na2 - (beta_Na11 * IC_Na2 + alpha_Na12 * IC_Na2 + alpha_Na3 * IC_Na2)
    in [1]
dot(IC_Na3) = beta_Na11 * IC_Na2 + beta_Na3 * C_Na3 - (alpha_Na11 * IC_Na3 + alpha_Na3 * IC_Na3)
    in [1]
dot(IF_Na) = alpha_Na2 * O_Na + beta_Na3 * C_Na1 + beta_Na4 * I1_Na + alpha_Na12 * IC_Na2 - (beta_Na2 * IF_Na + alpha_Na3 * IF_Na + alpha_Na4 * IF_Na + beta_Na12 * IF_Na)
    in [1]
dot(O_Na) = alpha_Na13 * C_Na1 + beta_Na2 * IF_Na - (beta_Na13 * O_Na + alpha_Na2 * O_Na)
    in [1]

k_alpha1 = 3.802
    in [mS/uF]
k_alpha2 = 0.1027
    in [1]
k_alpha3 = 2.5
    in [mV]
k_alpha4 = 150
    in [mV]
k_alpha5_11 = 17
    in [mV]
k_alpha5_12 = 2
    in [mV]
k_alpha5_13 = 3
    in [mV]
k_alpha6_11 = 0.2
    in [1]
k_alpha6_12 = 0.03
    in [1]
k_alpha6_13 = 0.02
    in [1]
k_alpha7 = 0.188495
    in [1]
k_alpha8 = 7
    in [mV]
k_alpha9 = 16.6
    in [mV]
k_alpha10 = 0.393956
    in [1]
k_alpha11 = 70
    in [mS/uF]
k_alpha12 = 7.7
    in [mV]
k_alpha13 = 10
    in [1]
k_alpha14 = 9.5
    in [1]
k_alpha15 = 7.0
    in [mV]

k_beta1 = 0.1917
    in [mS/uF]
k_beta2_11 = 2.5
    in [mV]
k_beta2_diff = 5
    in [mV]
#k_beta2_12 = -2.5
#    in [mV]
#k_beta2_13 = -7.5
#    in [mV]
k_beta3 = 20.3
    in [mV]
k_beta4 = 0.2
    in [mS/uF]
k_beta5 = 0.22
    in [mS/uF]
k_beta6 = 84
    in [mS/uF]
k_beta7 = 20
    in [1/mV/ms]
k_beta8 = 7
    in [mV]
k_beta9 = 50.0
    in [1]

alpha_Na11 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / k_alpha5_11) + k_alpha6_11 * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na12 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / (k_alpha5_11 - k_alpha5_12)) + (k_alpha6_11 + k_alpha6_12) * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na13 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / (k_alpha5_11 - k_alpha5_12 - k_alpha5_13)) + (k_alpha6_11 + k_alpha6_12 + k_alpha6_13) * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na2 = 1 [mS/uF] / (k_alpha7 * exp(-(V + k_alpha8) / k_alpha9) + k_alpha10)
    in [mS/uF]
alpha_Na3 = k_alpha11 * 1e-8 * exp(-(V + k_alpha15) / k_alpha12)
    in [mS/uF]
alpha_Na4 = alpha_Na2 / (k_alpha13 * 100)
    in [mS/uF]
alpha_Na5 = alpha_Na2 / (k_alpha14 * 10000)
    in [mS/uF]

beta_Na11 = k_beta1 * exp(-(V + k_beta2_11) / k_beta3)
    in [mS/uF]
beta_Na12 = k_beta4 * exp(-(V + (k_beta2_11 - k_beta2_diff)) / k_beta3)
    in [mS/uF]
beta_Na13 = k_beta5 * exp(-(V + (k_beta2_11 - 2*k_beta2_diff)) / k_beta3)
    in [mS/uF]
beta_Na2 = alpha_Na13 * alpha_Na2 * alpha_Na3 / (beta_Na13 * beta_Na3)
    in [mS/uF]
beta_Na3 = k_beta6 * 1e-4 + k_beta7 * 1e-6 * (V + k_beta8)
    in [mS/uF]
beta_Na4 = alpha_Na3
    in [mS/uF]
beta_Na5 = alpha_Na3 / k_beta9
    in [mS/uF]
