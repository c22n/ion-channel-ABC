[[model]]
name: Korhonen 2009 L-type calcium channel
author: Charles Houston
desc: """
      The Korhonen 2009 model for the L-type calcium current.
      """
ref: """
     T Korhonen et al. (2009) Model of Excitation-Contraction Coupling of Rat
     Neonatal Ventricular Myocytes.
     """
# Initial values
membrane.V = -67.0
ical.d = 1.40562700000000000e-3
ical.f = 0.99
ical.fCa = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ical.i_CaL
    in [mV]
T = 296
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
FoRT = F / (R * T)
Ca_o = 1800
    in [uM]


[ca_conc]
Ca_subSL = 0.115001
    in [uM]

[ical]
use membrane.V as V
use membrane.FoRT as FoRT
use membrane.F as F
use membrane.Ca_o as Ca_o
use ca_conc.Ca_subSL as Ca_subSL

i_CaL = g_CaL * 4 * V * F * FoRT * (Ca_subSL * exp(2 * V * FoRT) - 0.341 * Ca_o) / (exp(2 * V * FoRT) - 1)
    in [pA/pF]
G_CaL = 0.000063
    in [dm^3/F/ms]
g_CaL = G_CaL * d * f * fCa
    in [dm^3/F/ms]

dot(d) = (d_ss - d) / tau_d
    in [1]
d_ss = (1 + exp((p1 - V) / p2)) ^ (-1)
    in [1]
alpha_d = 1.4 * (1 + exp((-35 - V) / 13)) ^ (-1) + 0.25
    in [ms]
beta_d = 1.4 * (1 + exp((V + 5) / 5)) ^ (-1)
    in [ms]
gamma_d = (1 + exp((50 - V) / 20)) ^ (-1)
    in [ms]
tau_d = alpha_d * beta_d + gamma_d
    in [ms]
p1 = -11.1
p2 = 7.2

dot(f) = (f_ss - f) / tau_f
    in [1]
f_ss = (1 + exp((q1 + V) / q2)) ^ (-1)
    in [1]
tau_f = 1125 * exp(-(V + 27) ^ 2 / 240) + 165 * (1 + exp((25 - V) / 10)) ^ (-1) + 120
    in [ms]
q1 = 23.3
q2 = 5.4

dot(fCa) = k * (fCa_ss - fCa) / tau_fCa
    in [1]
alpha_fCa = (1 + (Ca_subSL / 0.4875) ^ 8) ^ (-1)
    in [1]
beta_fCa = 0.1 * (1 + exp((Ca_subSL - 0.5) / 0.1)) ^ (-1)
    in [1]
gamma_fCa = 0.2 * (1 + exp((Ca_subSL - 0.75) / 0.8)) ^ (-1)
    in [1]
fCa_ss = (alpha_fCa + beta_fCa + gamma_fCa + 0.23) / 1.46
    in [1]
tau_fCa = 10
    in [ms]
k = piecewise(fCa_ss > fCa and V > -60, 0, 1)
    in [1]