[[model]]
author: Charles Houston
desc: """
    Computer model of action potential of HL-1 myocytes.
    Initially developed from model of mouse ventricular myocytes by
    Bondarenko et al., 2004.
    """
name: houston_2017
# Initial values
membrane.V                                                       = -82.4202
calcium_concentration.Cai                                        = 0.115001
calcium_concentration.Cass                                       = 0.115001
calcium_concentration.CaJSR                                      = 1299.5
calcium_concentration.CaNSR                                      = 1299.5
calcium_fluxes.P_RyR                                             = 0
calcium_buffering.LTRPN_Ca                                       = 11.2684
calcium_buffering.HTRPN_Ca                                       = 125.29
ryr.P_O1                                         =  1.49102000000000000e-5
ryr.P_O2                                         =  9.51725999999999945e-11
ryr.P_C2                                         =  1.67739999999999990e-4
ical.O                                         =  9.30307999999999919e-19
ical.C2                                        =  1.24215999999999991e-4
ical.C3                                        =  5.78679000000000003e-9
ical.C4                                        =  1.19816000000000002e-13
ical.I1                                        =  4.97923000000000004e-19
ical.I2                                        =  3.45847000000000021e-14
ical.I3                                        =  1.85106000000000007e-14
sodium_concentration.Nai                                         = 14237.1
ina.C_Na2                                        = 0.020752
ina.C_Na1                                        =  2.79132000000000024e-4
ina.O_Na                                         =  7.13482999999999984e-7
ina.IF_Na                                        =  1.53176000000000012e-4
ina.I1_Na                                        =  6.73345000000000052e-7
ina.I2_Na                                        =  1.55786999999999992e-9
ina.IC_Na2                                       = 0.0113879
ina.IC_Na3                                       = 0.34278
potassium_concentration.Ki                                       = 143720
itof.ato_f                   =  2.65562999999999997e-3
itof.ito_f                   = 0.999977
itos.ato_s                   =  4.17069000000000001e-4
itos.ito_s                   = 0.998543
iks.nKs                     =  2.62753000000000010e-4
ikur.aur =  4.17069000000000001e-4
ikur.iur = 0.998543
ikss.aKss             =  4.17069000000000001e-4
ikss.iKss             = 1
ikr.C_K2                   =  6.41228999999999996e-4
ikr.C_K1                   =  9.92512999999999989e-4
ikr.O_K                    =  1.75298000000000001e-4
ikr.I_K                    =  3.19128999999999973e-5
icat.d = 0.01
icat.f = 0.99
iha.y = 0.01

# L-type calcium current
[ical]
C1 = 1 - (O + C2 + C3 + C4 + I1 + I2 + I3)
    in [1]
dot(C2) = 4 * alpha * C1 + 2 * beta * C3 - (beta * C2 + 3 * alpha * C2)
    in [1]
dot(C3) = 3 * alpha * C2 + 3 * beta * C4 - (2 * beta * C3 + 2 * alpha * C3)
    in [1]
dot(C4) = 2 * alpha * C3 + 4 * beta * O + 0.01 [ms] * (4 * Kpcb * beta * I1 - alpha * gamma * C4) + 0.002 * (4 * beta * I2 - Kpcf * C4) + 4 [ms] * beta * Kpcb * I3 - (3 * beta * C4 + alpha * C4 + 1 [ms] * gamma * Kpcf * C4)
    in [1]
dot(I1) = gamma * O + 0.001 * (alpha * I3 - Kpcf * I1) + 0.01 [ms] * (alpha * gamma * C4 - 4 * beta * Kpcf * I1) - Kpcb * I1
    in [1]
dot(I2) = 0.001 * (Kpcf * O - alpha * I2) + Kpcb * I3 + 0.002 * (Kpcf * C4 - 4 * beta * I2) - gamma * I2
    in [1]
dot(I3) = 0.001 * (Kpcf * I1 - alpha * I3) + gamma * I2 + 1 [ms] * gamma * Kpcf * C4 - (4 [ms] * beta * Kpcb * I3 + Kpcb * I3)
    in [1]
dot(O) = alpha * C4 + Kpcb * I1 + 0.001 * (alpha * I2 - Kpcf * O) - (4 * beta * O + gamma * O)
    in [1]
G_CaL = g_CaL * O
    in [mS/uF]
i_CaL = G_CaL * (membrane.V - E_CaL)
    in [pA/pF]

# Parameters
g_CaL = 0.1729
    in [mS/uF]
E_CaL = 33.2 # value reported for HL1-6 in Dias2014 (original: 63)
    in [mV]
i_CaL_max = 17.1 # value reported for HL1-6 in Dias2014 (original: 7)
    in [pA/pF]

alpha = kalpha1 * exp((membrane.V + kalpha2) / kalpha3) * (1 + kalpha4 * exp(-(membrane.V + kalpha5) ^ 2 / kalpha6) - kalpha7 * exp(-(membrane.V + kalpha8) ^ 2 / kalpha9)) / (1 + kalpha10 * exp((membrane.V + kalpha11) / kalpha12))
    in [mS/uF]
kalpha1 = 0.4 [mS/uF]
    in [mS/uF]
kalpha2 = 12 [mV]
    in [mV]
kalpha3 = 10 [mV]
    in [mV]
kalpha4 = 0.7
    in [1]
kalpha5 = 40 [mV]
    in [mV]
kalpha6 = 10 [mV^2]
    in [mV^2]
kalpha7 = 0.75
    in [1]
kalpha8 = 20 [mV]
    in [mV]
kalpha9 = 400 [mV^2]
    in [mV^2]
kalpha10 = 0.12
    in [1]
kalpha11 = 12 [mV]
    in [mV]
kalpha12 = 10 [mV]
    in [mV]

beta = kbeta1 * exp(-(membrane.V + kbeta2) / kbeta3)
    in [mS/uF]
kbeta1 = 0.05 [mS/uF]
    in [mS/uF]
kbeta2 = 12 [mV]
    in [mV]
kbeta3 = 13 [mV]
    in [mV]

gamma = Kpc_max * calcium_concentration.Cass / (Kpc_half + calcium_concentration.Cass)
    in [mS/uF]

Kpc_half = 20 [uM]
    in [uM]
Kpc_max = 0.23324 [mS/uF]
    in [mS/uF]
Kpcb = 0.0005 [mS/uF]
    in [mS/uF]

Kpcf = kKpcf1 * (1 - exp(-(membrane.V + kKpcf2) ^ 2 / kKpcf3))
    in [mS/uF]
kKpcf1 = 13 [mS/uF]
    in [mS/uF]
kKpcf2 = 14.5 [mV]
    in [mV]
kKpcf3 = 100 [mV^2]
    in [mV^2]



[iclca]
E_Cl = -40
    in [mV]
Km_Cl = 10
    in [uM]
O_ClCa = 0.2 / (1 + exp(-(membrane.V - 46.7 [mV]) / 7.8 [mV]))
    in [1]
g_ClCa = 10
    in [mS/uF]
i_ClCa = g_ClCa * O_ClCa * calcium_concentration.Cai / (calcium_concentration.Cai + Km_Cl) * (membrane.V - E_Cl)
    in [pA/pF]

[icab]
E_CaN = membrane.R * membrane.T / (2 * membrane.F) * log(membrane.Cao / calcium_concentration.Cai)
    in [mV]
g_Cab = 0.000367
    in [mS/uF]
i_Cab = g_Cab * (membrane.V - E_CaN)
    in [pA/pF]

[calcium_buffering]
dot(HTRPN_Ca) = calcium_fluxes.k_plus_htrpn * calcium_concentration.Cai * (HTRPN_tot - HTRPN_Ca) - calcium_fluxes.k_minus_htrpn * HTRPN_Ca
    in [uM]
HTRPN_tot = 140
    in [uM]
dot(LTRPN_Ca) = calcium_fluxes.k_plus_ltrpn * calcium_concentration.Cai * (LTRPN_tot - LTRPN_Ca) - calcium_fluxes.k_minus_ltrpn * LTRPN_Ca
    in [uM]
LTRPN_tot = 70
    in [uM]

[calcium_concentration]
BJSR = (1 + CSQN_tot * Km_CSQN / (Km_CSQN + CaJSR) ^ 2) ^ (-1)
    in [1]
Bi = (1 + CMDN_tot * Km_CMDN / (Km_CMDN + Cai) ^ 2) ^ (-1)
    in [1]
Bss = (1 + CMDN_tot * Km_CMDN / (Km_CMDN + Cass) ^ 2) ^ (-1)
    in [1]
CMDN_tot = 50
    in [uM]
CSQN_tot = 15000
    in [uM]
dot(CaJSR) = BJSR * (calcium_fluxes.J_tr - calcium_fluxes.J_rel)
    in [uM]
dot(CaNSR) = (calcium_fluxes.J_up - calcium_fluxes.J_leak) * membrane.Vmyo / membrane.VNSR - calcium_fluxes.J_tr * membrane.VJSR / membrane.VNSR
    in [uM]
dot(Cai) = Bi * (calcium_fluxes.J_leak + calcium_fluxes.J_xfer - (calcium_fluxes.J_up + calcium_fluxes.J_trpn + (icab.i_Cab + ipca.i_pCa - 2 * incx.i_NaCa + icat.i_CaT) * membrane.Acap * membrane.Cm / (2 * membrane.Vmyo * membrane.F)))
    in [uM]
dot(Cass) = Bss * (calcium_fluxes.J_rel * membrane.VJSR / membrane.Vss - (calcium_fluxes.J_xfer * membrane.Vmyo / membrane.Vss + ical.i_CaL * membrane.Acap * membrane.Cm / (2 * membrane.Vss * membrane.F)))
    in [uM]
Km_CMDN = 0.238
    in [uM]
Km_CSQN = 800
    in [uM]

[calcium_fluxes]
J_leak = v2 * (calcium_concentration.CaNSR - calcium_concentration.Cai)
    in [mol/m^3/s]
J_rel = v1 * (ryr.P_O1 + ryr.P_O2) * (calcium_concentration.CaJSR - calcium_concentration.Cass) * P_RyR
    in [mol/m^3/s]
J_tr = (calcium_concentration.CaNSR - calcium_concentration.CaJSR) / tau_tr
    in [mol/m^3/s]
J_trpn = k_plus_htrpn * calcium_concentration.Cai * (calcium_buffering.HTRPN_tot - calcium_buffering.HTRPN_Ca) + k_plus_ltrpn * calcium_concentration.Cai * (calcium_buffering.LTRPN_tot - calcium_buffering.LTRPN_Ca) - (k_minus_htrpn * calcium_buffering.HTRPN_Ca + k_minus_ltrpn * calcium_buffering.LTRPN_Ca)
    in [mol/m^3/s]
J_up = v3 * calcium_concentration.Cai ^ 2 / (Km_up ^ 2 + calcium_concentration.Cai ^ 2)
    in [mol/m^3/s]
J_xfer = (calcium_concentration.Cass - calcium_concentration.Cai) / tau_xfer
    in [mol/m^3/s]
Km_up = 0.5
    in [uM]
dot(P_RyR) = -0.04 [mS/uF] * P_RyR - 0.1 [mS/uF] * ical.i_CaL / ical.i_CaL_max * exp(-(membrane.V - 5 [mV]) ^ 2 / 648 [mV^2])
    in [1]
k_minus_htrpn = 3.2e-5
    in [mS/uF]
k_minus_ltrpn = 0.0196
    in [mS/uF]
k_plus_htrpn = 0.00237
    in [m^3/s/mol (1000000)]
k_plus_ltrpn = 0.0327
    in [m^3/s/mol (1000000)]
tau_tr = 20
    in [ms]
tau_xfer = 8
    in [ms]
v1 = 4.5
    in [mS/uF]
v2 = 1.74e-5
    in [mS/uF]
v3 = 0.45
    in [mol/m^3/s]

[ipca]
Km_pCa = 0.5
    in [uM]
i_pCa = i_pCa_max * calcium_concentration.Cai ^ 2 / (Km_pCa ^ 2 + calcium_concentration.Cai ^ 2)
    in [pA/pF]
i_pCa_max = 1
    in [pA/pF]

[environment]
time = 0 bind time
    in [ms]

[ina]
dot(C_Na1) = alpha_Na12 * C_Na2 + beta_Na13 * O_Na + alpha_Na3 * IF_Na - (beta_Na12 * C_Na1 + alpha_Na13 * C_Na1 + beta_Na3 * C_Na1)
    in [1]
dot(C_Na2) = alpha_Na11 * C_Na3 + beta_Na12 * C_Na1 + alpha_Na3 * IC_Na2 - (beta_Na11 * C_Na2 + alpha_Na12 * C_Na2 + beta_Na3 * C_Na2)
    in [1]
C_Na3 = 1 - (O_Na + C_Na1 + C_Na2 + IF_Na + I1_Na + I2_Na + IC_Na2 + IC_Na3)
    in [1]
E_Na = membrane.R * membrane.T / membrane.F * log((0.9 * membrane.Nao + 0.1 * membrane.Ko) / (0.9 * sodium_concentration.Nai + 0.1 * potassium_concentration.Ki))
    in [mV]
dot(I1_Na) = alpha_Na4 * IF_Na + beta_Na5 * I2_Na - (beta_Na4 * I1_Na + alpha_Na5 * I1_Na)
    in [1]
dot(I2_Na) = alpha_Na5 * I1_Na - beta_Na5 * I2_Na
    in [1]
dot(IC_Na2) = alpha_Na11 * IC_Na3 + beta_Na12 * IF_Na + beta_Na3 * C_Na2 - (beta_Na11 * IC_Na2 + alpha_Na12 * IC_Na2 + alpha_Na3 * IC_Na2)
    in [1]
dot(IC_Na3) = beta_Na11 * IC_Na2 + beta_Na3 * C_Na3 - (alpha_Na11 * IC_Na3 + alpha_Na3 * IC_Na3)
    in [1]
dot(IF_Na) = alpha_Na2 * O_Na + beta_Na3 * C_Na1 + beta_Na4 * I1_Na + alpha_Na12 * IC_Na2 - (beta_Na2 * IF_Na + alpha_Na3 * IF_Na + alpha_Na4 * IF_Na + beta_Na12 * IF_Na)
    in [1]
dot(O_Na) = alpha_Na13 * C_Na1 + beta_Na2 * IF_Na - (beta_Na13 * O_Na + alpha_Na2 * O_Na)
    in [1]
alpha_Na11 = 3.802 [mS/uF] / (0.1027 * exp(-(membrane.V + 2.5 [mV]) / 17 [mV]) + 0.2 * exp(-(membrane.V + 2.5 [mV]) / 150 [mV]))
    in [mS/uF]
alpha_Na12 = 3.802 [mS/uF] / (0.1027 * exp(-(membrane.V + 2.5 [mV]) / 15 [mV]) + 0.23 * exp(-(membrane.V + 2.5 [mV]) / 150 [mV]))
    in [mS/uF]
alpha_Na13 = 3.802 [mS/uF] / (0.1027 * exp(-(membrane.V + 2.5 [mV]) / 12 [mV]) + 0.25 * exp(-(membrane.V + 2.5 [mV]) / 150 [mV]))
    in [mS/uF]
alpha_Na2 = 1 [mS/uF] / (0.188495 * exp(-(membrane.V + 7 [mV]) / 16.6 [mV]) + 0.393956)
    in [mS/uF]
alpha_Na3 = 7e-7 [mS/uF] * exp(-(membrane.V + 7 [mV]) / 7.7 [mV])
    in [mS/uF]
alpha_Na4 = alpha_Na2 / 1000
    in [mS/uF]
alpha_Na5 = alpha_Na2 / 95000
    in [mS/uF]
beta_Na11 = 0.1917 [mS/uF] * exp(-(membrane.V + 2.5 [mV]) / 20.3 [mV])
    in [mS/uF]
beta_Na12 = 0.2 [mS/uF] * exp(-(membrane.V - 2.5 [mV]) / 20.3 [mV])
    in [mS/uF]
beta_Na13 = 0.22 [mS/uF] * exp(-(membrane.V - 7.5 [mV]) / 20.3 [mV])
    in [mS/uF]
beta_Na2 = alpha_Na13 * alpha_Na2 * alpha_Na3 / (beta_Na13 * beta_Na3)
    in [mS/uF]
beta_Na3 = 0.0084 [mS/uF] + 2e-5 [1/mV/ms] * (membrane.V + 7 [mV])
    in [mS/uF]
beta_Na4 = alpha_Na3
    in [mS/uF]
beta_Na5 = alpha_Na3 / 50
    in [mS/uF]
g_Na = 13
    in [mS/uF]
G_Na = g_Na * O_Na
    in [mS/uF]
i_Na = G_Na * (membrane.V - E_Na)
    in [pA/pF]

[itof]
E_K = membrane.R * membrane.T / membrane.F * log(membrane.Ko / potassium_concentration.Ki)
    in [mV]
alpha_a = 0.18064 [mS/uF] * exp(0.03577 [1/mV] * (membrane.V + 30 [mV]))
    in [mS/uF]
alpha_i = 0.000152 [mS/uF] * exp(-(membrane.V + 13.5 [mV]) / 7 [mV]) / (0.0067083 * exp(-(membrane.V + 33.5 [mV]) / 7 [mV]) + 1)
    in [mS/uF]
dot(ato_f) = alpha_a * (1 - ato_f) - beta_a * ato_f
    in [1]
beta_a = 0.3956 [mS/uF] * exp(-0.06237 [1/mV] * (membrane.V + 30 [mV]))
    in [mS/uF]
beta_i = 0.00095 [mS/uF] * exp((membrane.V + 33.5 [mV]) / 7 [mV]) / (0.051335 * exp((membrane.V + 33.5 [mV]) / 7 [mV]) + 1)
    in [mS/uF]
g_Kto_f = 0.0798
    in [mS/uF]
i_Kto_f = g_Kto_f * ato_f ^ 3 * ito_f * (membrane.V - E_K)
    in [pA/pF]
dot(ito_f) = alpha_i * (1 - ito_f) - beta_i * ito_f
    in [1]

[membrane]
Acap = 0.0001534
    in [cm^2]
Cao = 1800
    in [uM]
Cm = 1
    in [uF/cm^2]
F = 96.5
    in [C/mmol]
Ko = 5400
    in [uM]
Nao = 140000
    in [uM]
R = 8.314
    in [J/mol/K]
T = 298
    in [K]
dot(V) = -(ical.i_CaL + ipca.i_pCa + incx.i_NaCa + icab.i_Cab + ina.i_Na + inab.i_Nab + inak.i_NaK + itof.i_Kto_f + itos.i_Kto_s + ik1.i_K1 + iks.i_Ks + ikur.i_Kur + ikss.i_Kss + ikr.i_Kr + iclca.i_ClCa + icat.i_CaT + iha.i_ha + ikach.i_KAch) + i_stim
    in [mV]

VJSR = 1.2e-7
    in [uL]
VNSR = 2.098e-6
    in [uL]
Vmyo = 2.584e-5
    in [uL]
Vss = 1.485e-9
    in [uL]
i_stim = piecewise(environment.time >= stim_start and environment.time <= stim_end and environment.time - stim_start - floor((environment.time - stim_start) / stim_period) * stim_period <= stim_duration, stim_amplitude, 0 [pA/pF])
    in [pA/pF]
stim_amplitude = -80
    in [pA/pF]
stim_duration = 0.5
    in [ms]
stim_end = 100000
    in [ms]
stim_period = 71.43
    in [ms]
stim_start = 20
    in [ms]

[ikss]
dot(aKss) = (itos.ass - aKss) / tau_Kss
    in [1]
g_Kss = 0.0324
    in [mS/uF]
dot(iKss) = 0 [mS/uF]
    in [1]
i_Kss = g_Kss * aKss * iKss * (membrane.V - itof.E_K)
    in [pA/pF]
tau_Kss = 39.3 [ms] * exp(-0.0862 [1/mV] * membrane.V) + 13.17 [ms]
    in [ms]

[potassium_concentration]
dot(Ki) = -(itof.i_Kto_f + itos.i_Kto_s + ik1.i_K1 + iks.i_Ks + ikss.i_Kss + ikur.i_Kur + ikr.i_Kr - 2 * inak.i_NaK + iha.i_haK + ikach.i_KAch) * membrane.Acap * membrane.Cm / (membrane.Vmyo * membrane.F)
    in [uM]


[ikr]
C_K0 = 1 - (C_K1 + C_K2 + O_K + I_K)
    in [1]
dot(C_K1) = alpha_a0 * C_K0 + kb * C_K2 - (beta_a0 * C_K1 + kf * C_K1)
    in [1]
dot(C_K2) = kf * C_K1 + beta_a1 * O_K - (kb * C_K2 + alpha_a1 * C_K2)
    in [1]
dot(I_K) = alpha_i * O_K - beta_i * I_K
    in [1]
dot(O_K) = alpha_a1 * C_K2 + beta_i * I_K - (beta_a1 * O_K + alpha_i * O_K)
    in [1]
alpha_a0 = 0.022348 [mS/uF] * exp(0.01176 [1/mV] * membrane.V)
    in [mS/uF]
alpha_a1 = 0.013733 [mS/uF] * exp(0.038198 [1/mV] * membrane.V)
    in [mS/uF]
alpha_i = 0.090821 [mS/uF] * exp(0.023391 [1/mV] * (membrane.V + 5 [mV]))
    in [mS/uF]
beta_a0 = 0.047002 [mS/uF] * exp(-0.0631 [1/mV] * membrane.V)
    in [mS/uF]
beta_a1 = 6.89e-5 [mS/uF] * exp(-0.04178 [1/mV] * membrane.V)
    in [mS/uF]
beta_i = 0.006497 [mS/uF] * exp(-0.03268 [1/mV] * (membrane.V + 5 [mV]))
    in [mS/uF]
g_Kr = 0.078
    in [mS/uF]
i_Kr = g_Kr * O_K * (membrane.V - membrane.R * membrane.T / membrane.F * log((0.98 * membrane.Ko + 0.02 * membrane.Nao) / (0.98 * potassium_concentration.Ki + 0.02 * sodium_concentration.Nai)))
    in [pA/pF]
kb = 0.036778
    in [mS/uF]
kf = 0.023761
    in [mS/uF]

[ryr]
P_C1 = 1 - (P_C2 + P_O1 + P_O2)
    in [1]
dot(P_C2) = k_plus_c * P_O1 - k_minus_c * P_C2
    in [1]
dot(P_O1) = k_plus_a * calcium_concentration.Cass ^ n * P_C1 + k_minus_b * P_O2 + k_minus_c * P_C2 - (k_minus_a * P_O1 + k_plus_b * calcium_concentration.Cass ^ m * P_O1 + k_plus_c * P_O1)
    in [1]
dot(P_O2) = k_plus_b * calcium_concentration.Cass ^ m * P_O1 - k_minus_b * P_O2
    in [1]
k_minus_a = 0.07125
    in [mS/uF]
k_minus_b = 0.965
    in [mS/uF]
k_minus_c = 0.0008
    in [mS/uF]
k_plus_a = 0.006075
    in [m^12/s/mol^4 (1000000000000000)]
k_plus_b = 0.00405
    in [m^9/s/mol^3 (1000000000000)]
k_plus_c = 0.009
    in [mS/uF]
m = 3
    in [1]
n = 4
    in [1]

[iks]
alpha_n =  4.81332999999999970e-6 [1/mV/ms] * (membrane.V + 26.5 [mV]) / (1 - exp(-0.128 [1/mV] * (membrane.V + 26.5 [mV])))
    in [mS/uF]
beta_n =  9.53332999999999974e-5 [mS/uF] * exp(-0.038 [1/mV] * (membrane.V + 26.5 [mV]))
    in [mS/uF]
g_Ks = 0.00575
    in [mS/uF]
i_Ks = g_Ks * nKs ^ 2 * (membrane.V - itof.E_K)
    in [pA/pF]
dot(nKs) = alpha_n * (1 - nKs) - beta_n * nKs
    in [1]

[itos]
ass = 1 / (1 + exp(-(membrane.V + 22.5 [mV]) / 7.7 [mV]))
    in [1]
dot(ato_s) = (ass - ato_s) / tau_ta_s
    in [1]
g_Kto_s = 0.0629
    in [mS/uF]
i_Kto_s = g_Kto_s * ato_s * ito_s * (membrane.V - itof.E_K)
    in [pA/pF]
iss = 1 / (1 + exp((membrane.V + 45.2 [mV]) / 5.7 [mV]))
    in [1]
dot(ito_s) = (iss - ito_s) / tau_ti_s
    in [1]
tau_ta_s = 0.493 [ms] * exp(-0.0629 [1/mV] * membrane.V) + 2.058 [ms]
    in [ms]
tau_ti_s = 270 [ms] + 1050 [ms] / (1 + exp((membrane.V + 45.2 [mV]) / 5.7 [mV]))
    in [ms]

[inab]
g_Nab = 0.0026
    in [mS/uF]
i_Nab = g_Nab * (membrane.V - ina.E_Na)
    in [pA/pF]

[incx]
K_mCa = 1380
    in [uM]
K_mNa = 87500
    in [uM]
eta = 0.35
    in [1]
i_NaCa = k_NaCa * 1 / (K_mNa ^ 3 + membrane.Nao ^ 3) * 1 / (K_mCa + membrane.Cao) * 1 / (1 + k_sat * exp((eta - 1) * membrane.V * membrane.F / (membrane.R * membrane.T))) * (exp(eta * membrane.V * membrane.F / (membrane.R * membrane.T)) * sodium_concentration.Nai ^ 3 * membrane.Cao - exp((eta - 1) * membrane.V * membrane.F / (membrane.R * membrane.T)) * membrane.Nao ^ 3 * calcium_concentration.Cai)
    in [pA/pF]
k_NaCa = 292.8
    in [pA/pF]
k_sat = 0.1
    in [1]

[sodium_concentration]
dot(Nai) = -(ina.i_Na + inab.i_Nab + 3 * inak.i_NaK + 3 * incx.i_NaCa + iha.i_haNa) * membrane.Acap * membrane.Cm / (membrane.Vmyo * membrane.F)
    in [uM]

[inak]
Km_Ko = 1500
    in [uM]
Km_Nai = 21000
    in [uM]
f_NaK = 1 / (1 + 0.1245 * exp(-0.1 * membrane.V * membrane.F / (membrane.R * membrane.T)) + 0.0365 * sigma * exp(-membrane.V * membrane.F / (membrane.R * membrane.T)))
    in [1]
i_NaK = i_NaK_max * f_NaK * 1 / (1 + (Km_Nai / sodium_concentration.Nai) ^ 1.5) * membrane.Ko / (membrane.Ko + Km_Ko)
    in [pA/pF]
i_NaK_max = 0.88
    in [pA/pF]
sigma = 1 / 7 * (exp(membrane.Nao / 67300 [uM]) - 1)
    in [1]

[ik1]
i_K1 = 0.2938 [mS/uF] * membrane.Ko / (membrane.Ko + 210 [uM]) * (membrane.V - itof.E_K) / (1 + exp(0.0896 [1/mV] * (membrane.V - itof.E_K)))
    in [pA/pF]

[ikur]
dot(aur) = (itos.ass - aur) / tau_aur
    in [1]
g_Kur = 0.0975
    in [mS/uF]
G_Kur = g_Kur * aur * iur
    in [mS/uF]
i_Kur = G_Kur * (membrane.V - itof.E_K)
    in [pA/pF]
dot(iur) = (itos.iss - iur) / tau_iur
    in [1]
tau_aur = 0.493 [ms] * exp(-0.0629 [1/mV] * membrane.V) + 2.058 [ms]
    in [ms]
tau_iur = 1200 [ms] - 170 [ms] / (1 + exp((membrane.V + 45.2 [mV]) / 5.7 [mV]))
    in [ms]

[icat] # from Takeuchi2013
g_CaT = 0.4122
    in [mS/uF]
E_CaT = 45
    in [mV]
G_CaT = g_CaT * d * f
    in [mS/uF]
i_CaT = G_CaT * (membrane.V - E_CaT)
    in [pA/pF]

dot(d) = (dss - d)/tau_d
    in [1]
tau_d = (dtauk1*exp((membrane.V+dtauk2)/dtauk3) + dtauk4*exp(-(membrane.V+dtauk5)/dtauk6))^(-1)
    in [1]
dss = (1 + exp(-(membrane.V+dssk1)/dssk2))^(-1)
    in [1]

dssk1 = 30
    in [mV]
dssk2 = 6.0
    in [mV]
dtauk1 = 1.068
    in [1]
dtauk2 = 26.3
    in [mV]
dtauk3 = 30
    in [mV]
dtauk4 = 1.068
    in [1]
dtauk5 = 26.3
    in [mV]
dtauk6 = 30
    in [mV]

dot(f) = (fss - f)/tau_f
    in [1]
fss = (1 + exp((membrane.V+fssk1)/fssk2))^(-1)
    in [1]
tau_f = (ftauk1*exp(-(membrane.V+ftauk2)/ftauk3) + ftauk4*exp((membrane.V+ftauk5)/ftauk6))^(-1)
    in [1]

fssk1 = 48
    in [mV]
fssk2 = 7.0
    in [mV]
ftauk1 = 0.0153
    in [1]
ftauk2 = 61.7
    in [mV]
ftauk3 = 83.3
    in [mV]
ftauk4 = 0.015
    in [1]
ftauk5 = 61.7
    in [mV]
ftauk6 = 30
    in [mV]

[iha] # from Majumder2016
g_ha = 0.021 [mS/uF]
    in [mS/uF]

y_ss = 1 / (1 + exp((membrane.V + 78.65 [mV]) / 6.33 [mV]))
    in [1]
tau_y = 1000 [ms] / (0.11885 * exp((membrane.V + 75 [mV]) / 28.37 [mV]) + 0.56236 * exp((membrane.V + 75 [mV]) / -14.19 [mV]))
    in [ms]
dot(y) = (y_ss - y) / tau_y
    in [1]

G_ha = g_ha * y
    in [mS/uF]
i_haNa = G_ha * 0.2 * (membrane.V - ina.E_Na)
    in [pA/pF]
i_haK = G_ha * 0.8 * (membrane.V - itof.E_K)
    in [pA/pF]
i_ha = i_haNa + i_haK
    in [pA/pF]

[ikach] # from Majumder2016
g_Ach = 0.37488 [mS/uF]
    in [mS/uF]

G_Ach = g_Ach * (0.075 + 0.35 / (1 + exp((membrane.V + 102) / 10)))
    in [mS/uF]
i_KAch = G_Ach * (membrane.V - itof.E_K - 10)
    in [pA/pF]
