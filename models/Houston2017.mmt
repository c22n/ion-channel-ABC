[[model]]
author: Charles Houston
desc: """
    Computer model of action potential of HL-1 myocytes.
    Initially developed from model of mouse ventricular myocytes by
    Bondarenko et al., 2004.
    """
name: houston_2017
# Initial values
membrane.V                                                       = -67.0
calcium_concentration.Cai                                        = 0.115001
calcium_concentration.Cass                                       = 0.115001
calcium_concentration.CaJSR                                      = 1299.5
calcium_concentration.CaNSR                                      = 1299.5
calcium_fluxes.P_RyR                                             = 0
calcium_buffering.LTRPN_Ca                                       = 11.2684
calcium_buffering.HTRPN_Ca                                       = 125.29
ryr.P_O1                                         =  1.49102000000000000e-5
ryr.P_O2                                         =  9.51725999999999945e-11
ryr.P_C2                                         =  1.67739999999999990e-4
ical.O                                         =  9.30307999999999919e-19
ical.C2                                        =  1.24215999999999991e-4
ical.C3                                        =  5.78679000000000003e-9
ical.C4                                        =  1.19816000000000002e-13
ical.I1                                        =  4.97923000000000004e-19
ical.I2                                        =  3.45847000000000021e-14
ical.I3                                        =  1.85106000000000007e-14
ina.C_Na2 = 0.020752
ina.C_Na1 = 2.79132000000000024e-4
ina.O_Na = 7.13482999999999984e-7
ina.IF_Na = 1.53176000000000012e-4
ina.I1_Na = 6.73345000000000052e-7
ina.I2_Na = 1.55786999999999992e-9
ina.IC_Na2 = 0.0113879
ina.IC_Na3 = 0.34278
sodium_concentration.Nai                                         = 14237.1
potassium_concentration.Ki                                       = 143720
ito.xto = 4.05157399999999963e-3
ito.yto = 0.9945511
ikur.a_ur =  4.17069000000000001e-4
ikur.i_ur = 0.998543
ikss.aKss             =  4.17069000000000001e-4
ikss.iKss             = 1
ikr.xkr = 8.64138599999999917e-3
icat.d = 0.01
icat.f = 0.99
iha.y = 0.01



# L-type Ca2+ current - Bondarenko et al. (2004)
[ical]
C1 = 1 - (O + C2 + C3 + C4 + I1 + I2 + I3)
    in [1]
dot(C2) = 4 * alpha * C1 + 2 * beta * C3 - (beta * C2 + 3 * alpha * C2)
    in [1]
dot(C3) = 3 * alpha * C2 + 3 * beta * C4 - (2 * beta * C3 + 2 * alpha * C3)
    in [1]
dot(C4) = 2 * alpha * C3 + 4 * beta * O + 0.01 [ms] * (4 * Kpcb * beta * I1 - alpha * gamma * C4) + 0.002 * (4 * beta * I2 - Kpcf * C4) + 4 [ms] * beta * Kpcb * I3 - (3 * beta * C4 + alpha * C4 + 1 [ms] * gamma * Kpcf * C4)
    in [1]
dot(I1) = gamma * O + 0.001 * (alpha * I3 - Kpcf * I1) + 0.01 [ms] * (alpha * gamma * C4 - 4 * beta * Kpcf * I1) - Kpcb * I1
    in [1]
dot(I2) = 0.001 * (Kpcf * O - alpha * I2) + Kpcb * I3 + 0.002 * (Kpcf * C4 - 4 * beta * I2) - gamma * I2
    in [1]
dot(I3) = 0.001 * (Kpcf * I1 - alpha * I3) + gamma * I2 + 1 [ms] * gamma * Kpcf * C4 - (4 [ms] * beta * Kpcb * I3 + Kpcb * I3)
    in [1]
dot(O) = alpha * C4 + Kpcb * I1 + 0.001 * (alpha * I2 - Kpcf * O) - (4 * beta * O + gamma * O)
    in [1]
G_CaL = g_CaL * O
    in [mS/uF]
i_CaL = G_CaL * (membrane.V - E_CaL)
    in [pA/pF]

# Parameters
g_CaL = 0.1729
    in [mS/uF]
E_CaL = 33.2 # value reported for HL1-6 in Dias2014 (original: 63)
    in [mV]
i_CaL_max = 17.1 # value reported for HL1-6 in Dias2014 (original: 7)
    in [pA/pF]

alpha = kalpha1 * exp((membrane.V + kalpha2) / kalpha3) * (1 + kalpha4 * exp(-(membrane.V + kalpha5) ^ 2 / kalpha6) - kalpha7 * exp(-(membrane.V + kalpha8) ^ 2 / kalpha9)) / (1 + kalpha10 * exp((membrane.V + kalpha11) / kalpha12))
    in [mS/uF]
kalpha1 = 0.4 [mS/uF]
    in [mS/uF]
kalpha2 = 12 [mV]
    in [mV]
kalpha3 = 10 [mV]
    in [mV]
kalpha4 = 0.7
    in [1]
kalpha5 = 40 [mV]
    in [mV]
kalpha6 = 10 [mV^2]
    in [mV^2]
kalpha7 = 0.75
    in [1]
kalpha8 = 20 [mV]
    in [mV]
kalpha9 = 400 [mV^2]
    in [mV^2]
kalpha10 = 0.12
    in [1]
kalpha11 = 12 [mV]
    in [mV]
kalpha12 = 10 [mV]
    in [mV]

beta = kbeta1 * exp(-(membrane.V + kbeta2) / kbeta3)
    in [mS/uF]
kbeta1 = 0.05 [mS/uF]
    in [mS/uF]
kbeta2 = 12 [mV]
    in [mV]
kbeta3 = 13 [mV]
    in [mV]

gamma = Kpc_max * calcium_concentration.Cass / (Kpc_half + calcium_concentration.Cass)
    in [mS/uF]

Kpc_half = 20 [uM]
    in [uM]
Kpc_max = 0.23324 [mS/uF]
    in [mS/uF]
Kpcb = 0.0005 [mS/uF]
    in [mS/uF]

Kpcf = kKpcf1 * (1 - exp(-(membrane.V + kKpcf2) ^ 2 / kKpcf3))
    in [mS/uF]
kKpcf1 = 13 [mS/uF]
    in [mS/uF]
kKpcf2 = 14.5 [mV]
    in [mV]
kKpcf3 = 100 [mV^2]
    in [mV^2]


# Ca2+ activated chloride current - Bondarenko et al. (2004)
[iclca]
E_Cl = -40
    in [mV]
Km_Cl = 10
    in [uM]
O_ClCa = 0.2 / (1 + exp(-(membrane.V - 46.7 [mV]) / 7.8 [mV]))
    in [1]
g_ClCa = 10
    in [mS/uF]
i_ClCa = g_ClCa * O_ClCa * calcium_concentration.Cai / (calcium_concentration.Cai + Km_Cl) * (membrane.V - E_Cl)
    in [pA/pF]

# Ca2+ background current - Bondarenko et al. (2004)
[icab]
E_CaN = membrane.R * membrane.T / (2 * membrane.F) * log(membrane.Cao / calcium_concentration.Cai)
    in [mV]
g_Cab = 0.000367
    in [mS/uF]
i_Cab = g_Cab * (membrane.V - E_CaN)
    in [pA/pF]

# Ca2+ handling - Bondarenko et al. (2004)
[calcium_buffering]
dot(HTRPN_Ca) = calcium_fluxes.k_plus_htrpn * calcium_concentration.Cai * (HTRPN_tot - HTRPN_Ca) - calcium_fluxes.k_minus_htrpn * HTRPN_Ca
    in [uM]
HTRPN_tot = 140
    in [uM]
dot(LTRPN_Ca) = calcium_fluxes.k_plus_ltrpn * calcium_concentration.Cai * (LTRPN_tot - LTRPN_Ca) - calcium_fluxes.k_minus_ltrpn * LTRPN_Ca
    in [uM]
LTRPN_tot = 70
    in [uM]

[calcium_concentration]
BJSR = (1 + CSQN_tot * Km_CSQN / (Km_CSQN + CaJSR) ^ 2) ^ (-1)
    in [1]
Bi = (1 + CMDN_tot * Km_CMDN / (Km_CMDN + Cai) ^ 2) ^ (-1)
    in [1]
Bss = (1 + CMDN_tot * Km_CMDN / (Km_CMDN + Cass) ^ 2) ^ (-1)
    in [1]
CMDN_tot = 50
    in [uM]
CSQN_tot = 15000
    in [uM]
dot(CaJSR) = BJSR * (calcium_fluxes.J_tr - calcium_fluxes.J_rel)
    in [uM]
dot(CaNSR) = (calcium_fluxes.J_up - calcium_fluxes.J_leak) * membrane.Vmyo / membrane.VNSR - calcium_fluxes.J_tr * membrane.VJSR / membrane.VNSR
    in [uM]
dot(Cai) = Bi * (calcium_fluxes.J_leak + calcium_fluxes.J_xfer - (calcium_fluxes.J_up + calcium_fluxes.J_trpn + (icab.i_Cab + ipca.i_pCa - 2 * incx.i_NaCa + icat.i_CaT) * membrane.Acap * membrane.Cm / (2 * membrane.Vmyo * membrane.F)))
    in [uM]
dot(Cass) = Bss * (calcium_fluxes.J_rel * membrane.VJSR / membrane.Vss - (calcium_fluxes.J_xfer * membrane.Vmyo / membrane.Vss + ical.i_CaL * membrane.Acap * membrane.Cm / (2 * membrane.Vss * membrane.F)))
    in [uM]
Km_CMDN = 0.238
    in [uM]
Km_CSQN = 800
    in [uM]

[calcium_fluxes]
J_leak = v2 * (calcium_concentration.CaNSR - calcium_concentration.Cai)
    in [mol/m^3/s]
J_rel = v1 * (ryr.P_O1 + ryr.P_O2) * (calcium_concentration.CaJSR - calcium_concentration.Cass) * P_RyR
    in [mol/m^3/s]
J_tr = (calcium_concentration.CaNSR - calcium_concentration.CaJSR) / tau_tr
    in [mol/m^3/s]
J_trpn = k_plus_htrpn * calcium_concentration.Cai * (calcium_buffering.HTRPN_tot - calcium_buffering.HTRPN_Ca) + k_plus_ltrpn * calcium_concentration.Cai * (calcium_buffering.LTRPN_tot - calcium_buffering.LTRPN_Ca) - (k_minus_htrpn * calcium_buffering.HTRPN_Ca + k_minus_ltrpn * calcium_buffering.LTRPN_Ca)
    in [mol/m^3/s]
J_up = v3 * calcium_concentration.Cai ^ 2 / (Km_up ^ 2 + calcium_concentration.Cai ^ 2)
    in [mol/m^3/s]
J_xfer = (calcium_concentration.Cass - calcium_concentration.Cai) / tau_xfer
    in [mol/m^3/s]
Km_up = 0.5
    in [uM]
dot(P_RyR) = -0.04 [mS/uF] * P_RyR - 0.1 [mS/uF] * ical.i_CaL / ical.i_CaL_max * exp(-(membrane.V - 5 [mV]) ^ 2 / 648 [mV^2])
    in [1]
k_minus_htrpn = 3.2e-5
    in [mS/uF]
k_minus_ltrpn = 0.0196
    in [mS/uF]
k_plus_htrpn = 0.00237
    in [m^3/s/mol (1000000)]
k_plus_ltrpn = 0.0327
    in [m^3/s/mol (1000000)]
tau_tr = 20
    in [ms]
tau_xfer = 8
    in [ms]
v1 = 4.5
    in [mS/uF]
v2 = 1.74e-5
    in [mS/uF]
v3 = 0.45
    in [mol/m^3/s]


# Ca2+ pump current - Bondarenko et al. (2004)
[ipca]
Km_pCa = 0.5
    in [uM]
i_pCa = i_pCa_max * calcium_concentration.Cai ^ 2 / (Km_pCa ^ 2 + calcium_concentration.Cai ^ 2)
    in [pA/pF]
i_pCa_max = 1
    in [pA/pF]

[environment]
time = 0 bind time
    in [ms]


# Fast sodium current - Bondarenko et al. (2013)
[ina]
use membrane.V as V
g_Na = 13
    in [mS/uF]
E_Na = 58.686 # calculated from Nernst potential, 23.2 is value from Dias 2014
    in [mV]
G_Na = g_Na * O_Na
    in [mS/uF]
i_Na = G_Na * (V - E_Na)
    in [pA/pF]

dot(C_Na1) = alpha_Na12 * C_Na2 + beta_Na13 * O_Na + alpha_Na3 * IF_Na - (beta_Na12 * C_Na1 + alpha_Na13 * C_Na1 + beta_Na3 * C_Na1)
    in [1]
dot(C_Na2) = alpha_Na11 * C_Na3 + beta_Na12 * C_Na1 + alpha_Na3 * IC_Na2 - (beta_Na11 * C_Na2 + alpha_Na12 * C_Na2 + beta_Na3 * C_Na2)
    in [1]
C_Na3 = 1 - (O_Na + C_Na1 + C_Na2 + IF_Na + I1_Na + I2_Na + IC_Na2 + IC_Na3)
    in [1]
dot(I1_Na) = alpha_Na4 * IF_Na + beta_Na5 * I2_Na - (beta_Na4 * I1_Na + alpha_Na5 * I1_Na)
    in [1]
dot(I2_Na) = alpha_Na5 * I1_Na - beta_Na5 * I2_Na
    in [1]
dot(IC_Na2) = alpha_Na11 * IC_Na3 + beta_Na12 * IF_Na + beta_Na3 * C_Na2 - (beta_Na11 * IC_Na2 + alpha_Na12 * IC_Na2 + alpha_Na3 * IC_Na2)
    in [1]
dot(IC_Na3) = beta_Na11 * IC_Na2 + beta_Na3 * C_Na3 - (alpha_Na11 * IC_Na3 + alpha_Na3 * IC_Na3)
    in [1]
dot(IF_Na) = alpha_Na2 * O_Na + beta_Na3 * C_Na1 + beta_Na4 * I1_Na + alpha_Na12 * IC_Na2 - (beta_Na2 * IF_Na + alpha_Na3 * IF_Na + alpha_Na4 * IF_Na + beta_Na12 * IF_Na)
    in [1]
dot(O_Na) = alpha_Na13 * C_Na1 + beta_Na2 * IF_Na - (beta_Na13 * O_Na + alpha_Na2 * O_Na)
    in [1]

k_alpha1 = 3.802
    in [mS/uF]
k_alpha2 = 0.1027
    in [1]
k_alpha3 = 2.5
    in [mV]
k_alpha4 = 150
    in [mV]
k_alpha5 = 17
    in [mV]
k_alpha5_1 = 2
    in [mV]
k_alpha5_2 = 3
    in [mV]
k_alpha6 = 0.2
    in [1]
k_alpha6_1 = 0.03
    in [1]
k_alpha6_2 = 0.02
    in [1]
k_alpha7 = 0.188495
    in [1]
k_alpha8 = 16.6
    in [mV]
k_alpha9 = 0.393956
    in [1]
k_alpha10 = 70
    in [mS/uF]
k_alpha11 = 7.7
    in [mV]
k_alpha12 = 10
    in [1]
k_alpha13 = 95
    in [1]
k_alpha14 = 7.0
    in [mV]

k_beta1 = 0.1917
    in [mS/uF]
k_beta1_1 = 0.2 - 0.1917
    in [mS/uF]
k_beta1_2 = 0.22 - 0.1917
    in [mS/uF]
k_beta2 = 2.5
    in [mV]
k_beta2_diff = 5
    in [mV]
k_beta3 = 20.3
    in [mV]
k_beta4 = 84
    in [mS/uF]
k_beta5 = 20
    in [1/mV/ms]
k_beta6 = 50.0
    in [1]

alpha_Na11 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / k_alpha5) + k_alpha6 * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na12 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / (k_alpha5 - k_alpha5_1)) + (k_alpha6 + k_alpha6_1) * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na13 = k_alpha1 / (k_alpha2 * exp(-(V + k_alpha3) / (k_alpha5 - k_alpha5_1 - k_alpha5_2)) + (k_alpha6 + k_alpha6_1 + k_alpha6_2) * exp(-(V + k_alpha3) / k_alpha4))
    in [mS/uF]
alpha_Na2 = 1 [mS/uF] / (k_alpha7 * exp(-(V + k_alpha14) / k_alpha8) + k_alpha9)
    in [mS/uF]
alpha_Na3 = k_alpha10 * 1e-8 * exp(-(V + k_alpha14) / k_alpha11)
    in [mS/uF]
alpha_Na4 = alpha_Na2 / (k_alpha12 * 100)
    in [mS/uF]
alpha_Na5 = alpha_Na2 / (k_alpha13 * 1000)
    in [mS/uF]

beta_Na11 = k_beta1 * exp(-(V + k_beta2) / k_beta3)
    in [mS/uF]
beta_Na12 = (k_beta1 + k_beta1_1) * exp(-(V + (k_beta2 - k_beta2_diff)) / k_beta3)
    in [mS/uF]
beta_Na13 = (k_beta1 + k_beta1_2) * exp(-(V + (k_beta2 - 2 * k_beta2_diff)) / k_beta3)
    in [mS/uF]
beta_Na2 = alpha_Na13 * alpha_Na2 * alpha_Na3 / (beta_Na13 * beta_Na3)
    in [mS/uF]
beta_Na3 = k_beta4 * 1e-4 + k_beta5 * 1e-6 * (V + k_alpha14)
    in [mS/uF]
beta_Na4 = alpha_Na3
    in [mS/uF]
beta_Na5 = alpha_Na3 / k_beta6
    in [mS/uF]


[membrane]
Acap = 0.0001534
    in [cm^2]
# Extracellular ion concentrations
Cao = 1800
    in [uM]
Ko = 4500#5400
    in [uM]
Nao = 140000
    in [uM]
Ach = 0.1#10
    in [uM]
Cm = 1
    in [uF/cm^2]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
T = 298
    in [K]
FoRT = F / (R * T)
    in [1/mV]
dot(V) = -(ical.i_CaL + ipca.i_pCa + incx.i_NaCa + icab.i_Cab + ina.i_Na + inab.i_Nab + inak.i_NaK + ito.i_to + ik1.i_K1 + ikur.i_Kur + ikss.i_Kss + ikr.i_Kr + iclca.i_ClCa + icat.i_CaT + iha.i_ha + ikach.i_KAch + i_stim) / Cm
    in [mV]
i_stim = 0.0 [pA/pF]
    in [pA/pF]

VJSR = 1.2e-7
    in [uL]
VNSR = 2.098e-6
    in [uL]
Vmyo = 2.584e-5
    in [uL]
Vss = 1.485e-9
    in [uL]

# For Takeuchi et al. (2013) formulation of ical
# F_ss = 0.11
#     in [1]
# F_myo = 1 - F_ss
#     in  [1]


# i_stim = piecewise(environment.time >= stim_start and environment.time <= stim_end and environment.time - stim_start - floor((environment.time - stim_start) / stim_period) * stim_period <= stim_duration, stim_amplitude, 0 [pA/pF])
#     in [pA/pF]
# stim_amplitude = -80
#     in [pA/pF]
# stim_duration = 0.5
#     in [ms]
# stim_end = 100000
#     in [ms]
# stim_period = 71.43
#     in [ms]
# stim_start = 20
#     in [ms]

[ito]
g_to = 0.12375 [nS/pF]
     in [nS/pF]

xto_ss = 1 / (1 + exp(-(membrane.V - k_xss1) / k_xss2))
    in [1]
tau_xto = k_xtau1 + k_xtau2 * exp(-((membrane.V) / k_xtau3) ^ 2)
    in [ms]
yto_ss = 1 / (1 + exp((membrane.V + k_yss1) / k_yss2))
    in [1]
tau_yto = k_ytau1 + k_ytau2 * exp(-((membrane.V + k_ytau3) / k_ytau4) ^ 2)
    in [ms]
dot(xto) = (xto_ss - xto) / tau_xto
    in [1]
dot(yto) = (yto_ss - yto) / tau_yto
    in [1]
k_xss1 = 1 [mV]
    in [mV]
k_xss2 = 11 [mV]
    in [mV]
k_xtau1 = 1.5 [ms]
    in [ms]
k_xtau2 = 3.5 [ms]
    in [ms]
k_xtau3 = 30 [mV]
    in [mV]
k_yss1 = 40.5 [mV]
    in [mV]
k_yss2 = 11.5 [mV]
    in [mV]
k_ytau1 = 21.21 [ms]
    in [ms]
k_ytau2 = 38.4525 [ms]
    in [ms]
k_ytau3 = 52.45 [mV]
    in [mV]
k_ytau4 = 15.8827 [mV]
    in [mV]

G_to = g_to * xto * yto
    in [nS/pF]
i_to = G_to * (membrane.V - potassium_concentration.E_K)
    in [pA/pF]

# Steady-state K+ current - Bondarenko et al. (2004)
[ikss]
dot(aKss) = (ass - aKss) / tau_Kss
    in [1]
g_Kss = 0#0.0324
    in [mS/uF]
dot(iKss) = 0 [mS/uF]
    in [1]
i_Kss = g_Kss * aKss * iKss * (membrane.V - potassium_concentration.E_K)
    in [pA/pF]
ass = 1 / (1 + exp(-(membrane.V + 22.5 [mV]) / 7.7 [mV]))
    in [1]
tau_Kss = 39.3 [ms] * exp(-0.0862 [1/mV] * membrane.V) + 13.17 [ms]
    in [ms]

[potassium_concentration]
dot(Ki) = -(ito.i_to + ik1.i_K1 + ikss.i_Kss + ikur.i_Kur + ikr.i_Kr - 2 * inak.i_NaK + iha.i_haK + ikach.i_KAch) * membrane.Acap * membrane.Cm / (membrane.Vmyo * membrane.F)
    in [uM]
# Ki = 130000
#     in [uM]
E_K = membrane.R * membrane.T / membrane.F * log(membrane.Ko / Ki)
    in [mV]


# Rapid delayed rectifying K+ current - Takeuchi et al. (2013)
[ikr]
g_Kr = 0.73 [nS/pF]
    in [nS/pF]
xkr_ss = 1 / (1 + exp(-(membrane.V + k_xss1) / k_xss2))
    in [1]
tau_xkr = k_xtau1 * (k_xtau2 + k_xtau3 * exp(-((membrane.V + k_xtau4)/k_xtau5) ^ 2))
    in [ms]
rkr = 1 / (1 + exp((membrane.V + k_r1) / k_r2))
    in [1]
dot(xkr) = (xkr_ss - xkr) / tau_xkr
    in [1]

k_xss1 = 15 [mV]
    in [mV]
k_xss2 = 6 [mV]
    in [mV]
k_xtau1 = 2.5 [ms]
    in [ms]
k_xtau2 = 31.18
    in [1]
k_xtau3 = 217.18
    in [1]
k_xtau4 = 20.1376 [mV]
    in [mV]
k_xtau5 = 22.1996 [mV]
    in [mV]
k_r1 = 55 [mV]
    in [mV]
k_r2 = 24 [mV]
    in [mV]

G_Kr = g_Kr * sqrt(membrane.Ko / 5400 [uM]) * xkr * rkr
    in [nS/pF]
i_Kr = G_Kr * (membrane.V - potassium_concentration.E_K)
    in [pA/pF]


# Ryanodine receptors - Bondarenko et al. (2004)
[ryr]
P_C1 = 1 - (P_C2 + P_O1 + P_O2)
    in [1]
dot(P_C2) = k_plus_c * P_O1 - k_minus_c * P_C2
    in [1]
dot(P_O1) = k_plus_a * calcium_concentration.Cass ^ n * P_C1 + k_minus_b * P_O2 + k_minus_c * P_C2 - (k_minus_a * P_O1 + k_plus_b * calcium_concentration.Cass ^ m * P_O1 + k_plus_c * P_O1)
    in [1]
dot(P_O2) = k_plus_b * calcium_concentration.Cass ^ m * P_O1 - k_minus_b * P_O2
    in [1]
k_minus_a = 0.07125
    in [mS/uF]
k_minus_b = 0.965
    in [mS/uF]
k_minus_c = 0.0008
    in [mS/uF]
k_plus_a = 0.006075
    in [m^12/s/mol^4 (1000000000000000)]
k_plus_b = 0.00405
    in [m^9/s/mol^3 (1000000000000)]
k_plus_c = 0.009
    in [mS/uF]
m = 3
    in [1]
n = 4
    in [1]


# Background Na+ current - Bondarenko et al. (2004)
[inab]
g_Nab = 0.0026
    in [mS/uF]
i_Nab = g_Nab * (membrane.V - sodium_concentration.E_Na)
    in [pA/pF]


# Na+/Ca2+ exchange current - Bondarenko et al. (2004)
[incx]
K_mCa = 1380
    in [uM]
K_mNa = 87500
    in [uM]
eta = 0.35
    in [1]
i_NaCa = k_NaCa * 1 / (K_mNa ^ 3 + membrane.Nao ^ 3) * 1 / (K_mCa + membrane.Cao) * 1 / (1 + k_sat * exp((eta - 1) * membrane.V * membrane.F / (membrane.R * membrane.T))) * (exp(eta * membrane.V * membrane.F / (membrane.R * membrane.T)) * sodium_concentration.Nai ^ 3 * membrane.Cao - exp((eta - 1) * membrane.V * membrane.F / (membrane.R * membrane.T)) * membrane.Nao ^ 3 * calcium_concentration.Cai)
    in [pA/pF]
k_NaCa = 292.8
    in [pA/pF]
k_sat = 0.1
    in [1]

[sodium_concentration]
dot(Nai) = -(ina.i_Na + inab.i_Nab + 3 * inak.i_NaK + 3 * incx.i_NaCa + iha.i_haNa) * membrane.Acap * membrane.Cm / (membrane.Vmyo * membrane.F)
    in [uM]
E_Na = membrane.R * membrane.T / membrane.F * log(membrane.Nao / Nai)
    in [mV]


# Na+/K+ pump current - Bondarenko et al. (2004)
[inak]
Km_Ko = 1500
    in [uM]
Km_Nai = 21000
    in [uM]
f_NaK = 1 / (1 + 0.1245 * exp(-0.1 * membrane.V * membrane.F / (membrane.R * membrane.T)) + 0.0365 * sigma * exp(-membrane.V * membrane.F / (membrane.R * membrane.T)))
    in [1]
i_NaK = i_NaK_max * f_NaK * 1 / (1 + (Km_Nai / sodium_concentration.Nai) ^ 1.5) * membrane.Ko / (membrane.Ko + Km_Ko)
    in [pA/pF]
i_NaK_max = 0.88
    in [pA/pF]
sigma = 1 / 7 * (exp(membrane.Nao / 67300 [uM]) - 1)
    in [1]


# Inward rectifying K+ current - Bondarenko et al. (2004)
[ik1]
i_K1 = g_K1 * membrane.Ko / (membrane.Ko + k_1) * (membrane.V - potassium_concentration.E_K + k_3) / (1 + exp(k_2 * 1e-2 * (membrane.V - E_K1)))
    in [pA/pF]
E_K1 = potassium_concentration.E_K
    in [mV]
g_K1 = 0.2938 [mS/uF]
    in [mS/uF]
k_1 = 210 [uM]
    in [uM]
k_2 = 8.96 [1/mV]
    in [1/mV]
k_3 = 0 [mV]
    in [mV]



# Ultrarapidly activating delayed rectifying K+ current - Bondarenko et al.
# (2004)
[ikur]
use membrane.V as V
g_Kur = 0.0975
    in [mS/uF]
G_Kur = g_Kur * a_ur * i_ur
    in [mS/uF]
i_Kur = G_Kur * (V - potassium_concentration.E_K)
    in [pA/pF]

dot(a_ur) = (a_ur_ss - a_ur) / tau_a_ur
    in [1]
a_ur_ss = 1 / (1 + exp(-(V + k_ass1) / k_ass2))
    in [1]
tau_a_ur = k_atau1 * exp(-(k_atau2 * 1e-2 * V)) + k_atau3
    in [ms]

k_ass1 = 22.5
    in [mV]
k_ass2 = 7.7
    in [mV]
k_atau1 = 0.493
    in [ms]
k_atau2 = 6.29
    in [1/mV]
k_atau3 = 2.058
    in [mV]

dot(i_ur) = (i_ur_ss - i_ur) / tau_i_ur
    in [1]
i_ur_ss = 1 / (1 + exp((V + k_iss1) / k_iss2))
    in [1]
tau_i_ur = k_itau1 * 1000 - k_itau2 * 100 / (1 + exp((V + k_iss1) / k_iss2))
    in [ms]

k_iss1 = 45.2
    in [mV]
k_iss2 = 5.7
    in [mV]
k_itau1 = 1.2
    in [ms]
k_itau2 = 1.7
    in [ms]



# T-type Ca2+ current - Takeuchi et al. (2013)
[icat]
use membrane.V as V
g_CaT = 0.4122
    in [mS/uF]
E_CaT = 45 # Dias et al., 2014 report 33mV for HL1-6
    in [mV]
G_CaT = g_CaT * d * f
    in [mS/uF]
i_CaT = G_CaT * (V - E_CaT)
    in [pA/pF]

dot(d) = (dss - d)/tau_d
    in [1]
tau_d = (k_dtau1*exp((V+k_dtau2)/k_dtau3) + k_dtau4*exp(-(V+k_dtau5)/k_dtau6))^(-1)
    in [1]
dss = (1 + exp(-(V+k_dss1)/k_dss2))^(-1)
    in [1]

k_dss1 = 30
    in [mV]
k_dss2 = 6.0
    in [mV]
k_dtau1 = 1.068
    in [1]
k_dtau2 = 26.3
    in [mV]
k_dtau3 = 30
    in [mV]
k_dtau4 = 1.068
    in [1]
k_dtau5 = 26.3
    in [mV]
k_dtau6 = 30
    in [mV]

dot(f) = (fss - f)/tau_f
    in [1]
fss = (1 + exp((V+k_fss1)/k_fss2))^(-1)
    in [1]
tau_f = (k_ftau1 * 1e-2 * exp(-(V+k_ftau2)/k_ftau3) + k_ftau4 * 1e-2 * exp((V+k_ftau5)/k_ftau6))^(-1)
    in [1]

k_fss1 = 48
    in [mV]
k_fss2 = 7.0
    in [mV]
k_ftau1 = 1.53
     in [1]
k_ftau2 = 61.7
     in [mV]
k_ftau3 = 83.3
     in [mV]
k_ftau4 = 1.5
     in [1]
k_ftau5 = 61.7
     in [mV]
k_ftau6 = 30
     in [mV]


# Hyperpolarisation activated nonselective cation current - Majumder et al.
# (2016)
[iha]
g_ha = 0.021 [mS/uF]
    in [mS/uF]

y_ss = 1 / (1 + exp((membrane.V + k_yss1) / k_yss2))
    in [1]
k_yss1 = 78.65 [mV]
    in [mV]
k_yss2 = 6.33 [mV]
    in [mV]

tau_y = 1000 * k_ytau1 / (k_ytau2 * exp((membrane.V + k_ytau3) / k_ytau4) + k_ytau5 * exp((membrane.V + k_ytau6) / -k_ytau7))
    in [ms]
k_ytau1 = 1 [ms]
    in [ms]
k_ytau2 = 0.11885
    in [1]
k_ytau3 = 75 [mV]
    in [mV]
k_ytau4 = 28.37 [mV]
    in [mV]
k_ytau5 = 0.56236
    in [1]
k_ytau6 = 75 [mV]
    in [mV]
k_ytau7 = 14.19 [mV]
    in [mV]

dot(y) = (y_ss - y) / tau_y
    in [1]

G_ha = g_ha * y
    in [mS/uF]
i_haNa = G_ha * k_i_haNa * (membrane.V - sodium_concentration.E_Na)
    in [pA/pF]
i_haK = G_ha * (1-k_i_haNa) * (membrane.V - potassium_concentration.E_K)
    in [pA/pF]

k_i_haNa = 0.2
    in [1]

i_ha = i_haNa + i_haK
    in [pA/pF]


# Acetylcholine sensitive K+ current - Majumder et al. (2016)
[ikach]
g_KAch = 0.37488 [mS/uF]
    in [mS/uF]

i_KAch = (k_1 / (1.0 + k_2 / membrane.Ach ^ k_3)) * (k_4 + k_5 / (1.0 + exp((membrane.V + k_6) / k_7))) * (membrane.V - potassium_concentration.E_K - k_8)
k_1 = 3.5
    in [1]
k_2 = 9.13652
    in [1]
k_3 = 0.477811
    in [1]
k_4 = 0.04
    in [1]
k_5 = 0.23
    in [1]
k_6 = 102 [mV]
    in [mV]
k_7 = 10 [mV]
    in [mV]
k_8 = 10 [mV]
    in [mV]
