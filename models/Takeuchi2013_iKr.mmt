[[model]]
name: Takeuchi 2013 Rapidly activating delayed rectifier K Current
author: Charles Houston

# Initial values
membrane.V = -67.0
ikr.xkr = 8.64138599999999917e-3

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ikr.i_Kr
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
Ko = 5400
    in [uM]
Ki = 143720
    in [uM]
Nao = 140000
    in [uM]
Nai = 14237.1
    in [uM]

[ikr]
g_Kr = 0.73 [nS/pF]
    in [nS/pF]
xkr_ss = 1 / (1 + exp(-(membrane.V + k_xss1) / k_xss2))
    in [1]
tau_xkr = k_xtau1 * (k_xtau2 + k_xtau3 * exp(-((membrane.V + k_xtau4)/k_xtau5) ^ 2))
    in [ms]
rkr = 1 / (1 + exp((membrane.V + k_r1) / k_r2))
    in [1]
dot(xkr) = (xkr_ss - xkr) / tau_xkr
    in [1]

k_xss1 = 15 [mV]
    in [mV]
k_xss2 = 6 [mV]
    in [mV]
k_xtau1 = 2.5 [ms]
    in [ms]
k_xtau2 = 31.18
    in [1]
k_xtau3 = 217.18
    in [1]
k_xtau4 = 20.1376 [mV]
    in [mV]
k_xtau5 = 22.1996 [mV]
    in [mV]
k_r1 = 55 [mV]
    in [mV]
k_r2 = 24 [mV]
    in [mV]

E_K = membrane.R * membrane.T / membrane.F * log(membrane.Ko / membrane.Ki)
    in [mV]
G_Kr = g_Kr * sqrt(membrane.Ko / 5400 [uM]) * xkr * rkr
    in [nS/pF]
i_Kr = G_Kr * (membrane.V - E_K)
    in [pA/pF]
