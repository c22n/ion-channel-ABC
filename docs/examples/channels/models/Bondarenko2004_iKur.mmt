[[model]]
name: Bondarenko 2004 Rapidly activating delayed rectifier K Current
author: Charles Houston

# Initial values
membrane.V = -67.0
ikur.a_ur                 = 4.17069000000000001e-4
ikur.i_ur                 = 0.998543

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ikur.i_Kur
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
Ko = 4000
    in [uM]
Ki = 120000
    in [uM]

[ikur]
use membrane.V as V
g_Kur = 0.0975
    in [mS/uF]
E_K = (membrane.R * membrane.T / membrane.F) * log(membrane.Ko / membrane.Ki)
    in [mV]
G_Kur = g_Kur * a_ur * i_ur
    in [mS/uF]
i_Kur = G_Kur * (V - E_K)
    in [pA/pF]

dot(a_ur) = (a_ur_ss - a_ur) / tau_a_ur
    in [1]
a_ur_ss = 1 / (1 + exp(-(V + k_ass1) / k_ass2))
    in [1]
tau_a_ur = k_atau1 * exp(-(k_atau2 * 1e-2 * V)) + k_atau3
    in [ms]

k_ass1 = 22.5
    in [mV]
k_ass2 = 7.7
    in [mV]
k_atau1 = 0.493
    in [ms]
k_atau2 = 6.29
    in [1/mV]
k_atau3 = 2.058
    in [mV]

dot(i_ur) = (i_ur_ss - i_ur) / tau_i_ur
    in [1]
i_ur_ss = 1 / (1 + exp((V + k_iss1) / k_iss2))
    in [1]
tau_i_ur = k_itau1 * 1000 - k_itau2 * 100 / (1 + exp((V + k_iss1) / k_iss2))
    in [ms]

k_iss1 = 45.2
    in [mV]
k_iss2 = 5.7
    in [mV]
k_itau1 = 1.2
    in [ms]
k_itau2 = 1.7
    in [ms]
