[[model]]
name: Fast sodium channel
author: Charles Houston
desc: """
      Generic model for ina.
      """

# Initial values
membrane.V = -67.0
ina.m = 0.01
ina.h = 0.99
ina.j = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ina.i_Na
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
FoRT = F / (R * T)
    in [1/mV]
Nao = 145000
    in [uM]
Nai = 10000
    in [uM]

[na_conc]
use membrane.Nao as Nao
use membrane.Nai as Nai
use membrane.FoRT as FoRT

E_Na = (1 / FoRT) * log(Nao / Nai)
    in [mV]

[ina]
use membrane.V as V
use na_conc.E_Na as E_Na

g_Na = 35
    in [mS/uF]
G_Na = g_Na * m ^ 3 * h * j
    in [mS/uF]
i_Na = G_Na * (V - E_Na)
    in [pA/pF]

dot(m) = (mss - m) / tau_m
    in [1]
mss = (1 + exp((Vhalf_m - V) / k_m)) ^ (-1)
    in [1]
tau_m = c_bm + c_am * exp(-1 * (Vmax_m - V)^2 / (sigma_m^2))
    in [ms]
Vhalf_m = -45
k_m = 6.5
c_bm = 0.02
c_am = 0.12
sigma_m = 25
Vmax_m = -40

dot(h) = (hss - h) / tau_h
    in [1]
hss = (1 + exp((Vhalf_h - V) / k_h)) ^ (-1)
    in [1]
tau_h = c_bh + c_ah * exp(-1 * (Vmax_h - V)^2 / (sigma_h^2))
    in [ms]
Vhalf_h = -76.1
k_h = -6.07
c_bh = 0.01
c_ah = 30.0
Vmax_h = -60
sigma_h = 25

dot(j) = (jss - j) / tau_j
    in [1]
jss = hss
    in [1]
tau_j = c_bj + c_aj * exp(-1 * (Vmax_j - V)^2 / (sigma_j^2))
    in [ms]
c_bj = 5.0
c_aj = 75.0
Vmax_j = -60
sigma_j = 25
