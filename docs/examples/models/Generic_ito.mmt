[[model]]
name: Takeuchi 2013 transient outward K Current
author: Charles Houston

# Initial values
membrane.V = -67.0
ito.x = 0.01
ito.y = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ito.i_to
    in [mV]
T = 310
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
FoRT = F / (R * T)
    in [1/mV]
Ko = 5400
    in [uM]
Ki = 143720
    in [uM]

[k_conc]
use membrane.FoRT as FoRT
use membrane.Ko as Ko
use membrane.Ki as Ki

E_K = (1 / FoRT) * log(Ko / Ki)
    in [mV]

[ito]
use membrane.V as V
use k_conc.E_K as E_K

g_to = 0.12375
     in [nS/pF]
G_to = g_to * x * y
    in [nS/pF]
i_to = G_to * (V - E_K)
    in [pA/pF]

dot(x) = (xss - x) / tau_x
    in [1]
xss = (1 + exp((Vhalf_x - V) / k_x)) ^ (-1)
    in [1]
tau_x = c_bx + c_ax * exp(-1 * (Vmax_x - V)^2 / (sigma_x^2))
    in [ms]
Vhalf_x = -30
k_x = 11
c_bx = 1.5
c_ax = 3.5
Vmax_x = -10
sigma_x = 5.5

dot(y) = (yss - y) / tau_y
    in [1]
yss = (1 + exp((Vhalf_y - V) / k_y)) ^ (-1)
    in [1]
tau_y = c_by + c_ay * exp(-1 * (Vmax_y - V)^2 / (sigma_y^2))
    in [ms]
Vhalf_y = -40.5
k_y = -11.5
c_by = 21.21
c_ay = 38.4525
sigma_y = 4
Vmax_y = -52
