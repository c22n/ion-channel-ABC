[[model]]
name: T-type Ca2+ channel model
author: Charles Houston
desc: """
      Markov model for the T-type calcium channel.
      """
# Initial values
membrane.V = -67
icat.a = 0.01
icat.r = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -icat.i_CaT
    in [mV]
Ca_o = 5000
    in [uM]
Ca_subSL = 0.15001
    in [uM]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
T = 305
    in [K]
FoRT = F / (R * T)
    in [1/mV]

[ca_conc]
use membrane.FoRT as FoRT
use membrane.Ca_o as Ca_o
use membrane.Ca_subSL as Ca_subSL
    
E_Ca = 1 / (2 * FoRT) * log(Ca_o / Ca_subSL)
    in [mV]

[icat]
use membrane.V as V
use ca_conc.E_Ca as E_Ca

g_CaT = 1.0
    in [mS/uF]
g = a * r
    in [1]
i_CaT = g_CaT * g * (V - E_Ca)
    in [pA/pF]

dot(a) = (a_ss - a) / tau_a
    in [1]
tau_a = 1 / (k_1 + k_2)
    in [ms]
a_ss = k_1 * tau_a
    in [1]

dot(r) = (r_ss - r) / tau_r
    in [1]
tau_r = 1 / (k_3 + k_4)
    in [ms]
r_ss = k_4 * tau_r
    in [1]

k_1 = exp(p_1) * exp(p_2 * V)
    in [1/ms]
k_2 = exp(p_3) * exp(-p_4 * V)
    in [1/ms]
k_3 = exp(p_5) * exp(p_6 * V)
    in [1/ms]
k_4 = exp(p_7) * exp(-p_8 * V)
    in [1/ms]

p_1 = -9
    in [1/ms]
p_2 = 0.1 
    in [1/mV]
p_3 = -10
    in [1/ms]
p_4 = 0.05
    in [1/mV]
p_5 = -3
    in [1/ms]
p_6 = 0.01
    in [1/mV]
p_7 = -5
    in [1/ms]
p_8 = 0.05
    in [1/mV]