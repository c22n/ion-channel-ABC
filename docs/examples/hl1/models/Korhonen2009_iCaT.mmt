[[model]]
name: Korhonen 2009 T-type calcium channel
author: Charles Houston
desc: """
      The Korhonen 2009 model for the T-type calcium channel in NRVMs.
      """
# Initial values
membrane.V = -67
icat.a = 0.0
icat.i = 1.0

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -icat.i_CaT
    in [mV]

[extra]
Ca_o = 5000 [uM]

[calcium]
use phys.FoRT as FoRT
use extra.Ca_o as Ca_o

Ca_i = 0.15001 [uM]
E_Ca = 1 / (2 * FoRT) * log(Ca_o/Ca_i)
    in [mV]

[phys]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
T = 305 [K]
FoRT = F / (R * T)

[icat]
use membrane.V as V
use calcium.E_Ca as E_Ca

g_CaT = 0.2 [mS/uF]
E_CaT = 33 [mV]
v_offset = 106.5 [mV]
g = a * i
    in [1]
i_CaT = g_CaT * g * (V - E_Ca + v_offset)
    in [pA/pF]

dot(a) = (ass - a) / tau_a
    in [1]
ass = (1 + exp(-(V + p1) / p2)) ^ (-1)
    in [mV]
tau_a = p3 + p4 * (1 + exp(p5 * (V + p6))) ^ (-1)
    in [ms]

p1 = 37.49098
p2 = 5.40634
p3 = 0.6
p4 = 5.4
p5 = 0.03
p6 = 100

dot(i) = (iss - i) / tau_i
    in [1]
iss = (1 + exp((V + q1) / q2)) ^ (-1)
    in [mV]
tau_i = q3 + q4 * (1 + exp(q5 * (V + q6))) ^ (-1)
    in [ms]

q1 = 66
q2 = 6
q3 = 1
q4 = 40
q5 = 0.08
q6 = 65
