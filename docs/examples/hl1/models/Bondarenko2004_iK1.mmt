[[model]]
name: Bondarenko 2004 Inward rectifying K Current (i_K1)
author: Charles Houston

# Initial values
membrane.V = -67.0

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ik1.i_k1
    in [mV]

[phys]
T = 310 [K]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
FoRT = F/(R*T)

[extra]
K_o = 5400 [uM]

[potassium]
use phys.FoRT as FoRT
use extra.K_o as K_o

K_i = 143720 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[ik1]
use extra.K_o as K_o
use membrane.V as V
use potassium.E_K as E_K

i_k1 = g_K1 * K_o / (K_o + k_1) * (V - E_K + k_3) / (1 + exp(k_2 * 1e-2 * (V - E_K + k_3)))
    in [pA/pF]

g_K1 = 0.2938 [mS/uF]
k_1 = 210 [uM]
k_2 = 8.96 [1/mV]
k_3 = 0 [mV]
