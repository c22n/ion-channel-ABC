[[model]]
name: Simple model for hyperpolarisation-activated non-specific cation current
author: Charles Houston
desc: """
      Simple model for the `funny' current.
      """
# Initial values
membrane.V = -67
iha.x = 0.01

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -iha.i_ha
    in [mV]
T = 295
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
FoRT = F / (R * T)
    in [1/mV]
K_o = 25000
    in [uM]
K_i = 120000
    in [uM]
Na_i = 5400
    in [uM]
Na_o = 140000
    in [uM]

[k_conc]
use membrane.K_o as K_o
use membrane.K_i as K_i
use membrane.FoRT as FoRT

E_K = (1 / FoRT) * log(K_o / K_i)
    in [mV]

[na_conc]
use membrane.Na_o as Na_o
use membrane.Na_i as Na_i
use membrane.FoRT as FoRT

E_Na = (1 / FoRT) * log(Na_o / Na_i)
    in [mV]

[iha]
use membrane.V as V
use na_conc.E_Na as E_Na
use k_conc.E_K as E_K

g_ha = 0.021
    in [mS/uF]
g = g_ha * x
    in [mS/uF]
i_haNa = g * k_haNa * (V - E_Na)
    in [pA/pF]
i_haK = g * (1-k_haNa) * (V - E_K)
    in [pA/pF]
i_ha = i_haNa + i_haK
    in [pA/pF]

k_haNa = 0.2
    in [1]

dot(x) = (x_ss - x) / tau_x
    in [1]
tau_x = 1 / (k_1 + k_2)
    in [ms]
x_ss = k_2 * tau_x
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]

p_1 = 1e-4
    in [1/ms]
p_2 = 0.1 
    in [1/mV]
p_3 = 4e-5
    in [1/ms]
p_4 = 0.05
    in [1/mV]
