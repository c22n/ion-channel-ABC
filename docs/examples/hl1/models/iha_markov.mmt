[[model]]
name: Simple model for hyperpolarisation-activated non-specific cation current
author: Charles Houston
desc: """
      Simple model for the hyperpolarisation activated channel current.
      """
# Initial values
membrane.V = -67
iha.x = 0.01

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -iha.i_ha
    in [mV]

[phys]
T = 296 [K]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
FoRT = F/(R*T)

[extra]
K_o = 25000 [uM]
Na_o = 140000 [uM]

[potassium]
use extra.K_o as K_o
use phys.FoRT as FoRT

K_i = 120000 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[sodium]
use extra.Na_o as Na_o
use phys.FoRT as FoRT

Na_i = 5400 [uM]
E_Na = 1/FoRT * log(Na_o/Na_i)
    in [mV]

[iha]
use membrane.V as V
use sodium.E_Na as E_Na
use potassium.E_K as E_K

g_ha = 0.021 [mS/uF]
g = x
    in [1]
i_haNa = g_ha * g * k_haNa * (V - E_Na)
    in [pA/pF]
i_haK = g_ha * g * (1-k_haNa) * (V - E_K)
    in [pA/pF]
i_ha = i_haNa + i_haK
    in [pA/pF]

k_haNa = 0.2

dot(x) = (x_ss - x) / tau_x
    in [1]
tau_x = 1 / (k_1 + k_2)
    in [ms]
x_ss = k_2 * tau_x
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]

p_1 = 1e-4 [1/ms]
p_2 = 0.1 [1/mV]
p_3 = 4e-5 [1/ms]
p_4 = 0.05 [1/mV]
