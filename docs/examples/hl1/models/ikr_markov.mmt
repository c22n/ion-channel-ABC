[[model]]
name: Delayed rectifier K (iKr) channel model
author: Charles Houston
desc: """
      Simple model for iKr.
      """
# Initial values
membrane.V = -67
ikr.xf = 0.0
ikr.xs = 0.0
ikr.r = 1.0

[engine]
time = 0 bind time
    in [ms]

[extra]
K_o = 5400 [uM]

[phys]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
T = 296 [K]
FoRT = F / (R * T)

[membrane]
dot(V) = -ikr.i_Kr
    in [mV]

[potassium]
use phys.FoRT as FoRT
use extra.K_o as K_o
   
K_i = 143e3 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[ikr]
use membrane.V as V
use potassium.E_K as E_K
use extra.K_o as K_o

g_Kr = 0.5 [mS/uF]
g = (Arel*xf + (1-Arel)*xs) * r
    in [1]
i_Kr = g_Kr * sqrt(K_o/5400) * g * (V - E_K)
    in [pA/pF]

dot(xf) = (xf_ss - xf) / tau_xf
    in [1]
tau_xf = 1 / (k_1 + k_2)
    in [ms]
xf_ss = k_1 * tau_xf
    in [1]

dot(xs) = (xs_ss - xs) / tau_xs
    in [1]
tau_xs = 1 / (k_3 + k_4)
    in [ms]
xs_ss = k_3 * tau_xs
    in [1]

dot(r) = (r_ss - r) / tau_r
    in [1]
tau_r = 1 / (k_5 + k_6)
    in [ms]
r_ss = k_6 * tau_r
    in [1]

# Model relative amplitude of fast and slow time constant
# using Boltzmann curve
Arel = (1 + exp(s_A*(h_A+V)))^(-1)
    in [1]
s_A = 0.02
    in [1]
h_A = 20
    in [mV]


k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]
k_3 = p_5*exp(p_6 * V)
    in [1/ms]
k_4 = p_7*exp(-p_8 * V)
    in [1/ms]
k_5 = p_9*exp(p_10 * V)
    in [1/ms]
k_6 = p_11*exp(-p_12 * V)
    in [1/ms]

p_1 = 1e-4 [1/ms]
p_2 = 0.1 [1/mV]
p_3 = 4e-5 [1/ms]
p_4 = 0.05 [1/mV]
p_5 = 5e-2 [1/ms]
p_6 = 0.01 [1/mV]
# from equal steady-state activation gate
# p_2+p_4==p_6+p_8
# log(p_1)+log(p_3)==log(p_5)+log(p_7)
p_7 = p_3*p_5/p_1
p_8 = p_2+p_4-p_6
p_9 = 5e-2 [1/ms]
p_10 = 0.01 [1/mV]
p_11 = 6e-3 [1/ms]
p_12 = 0.05 [1/mV]
