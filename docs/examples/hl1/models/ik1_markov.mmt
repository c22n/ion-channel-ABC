[[model]]
name: Simple model for inward rectifier ik1 current
author: Charles Houston
desc: """
      Simple model for ik1.
      """
# Initial values
membrane.V = -67

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ik1.i_k1
    in [mV]

[phys]
T = 296 [K]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
FoRT = F/(R*T)

[extra]
K_o = 25000 [uM]

[potassium]
use extra.K_o as K_o
use phys.FoRT as FoRT

K_i = 120000 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[ik1]
use membrane.V as V
use potassium.E_K as E_K
use extra.K_o as K_o

g_k1 = 0.021 [mS/uF]
g = x_ss
    in [1]
i_k1 = g_k1 * g * sqrt(K_o/5400) * (V - E_K)
    in [pA/pF]

x_ss = k_2 / (k_1 + k_2)
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]

p_1 = 1e-4 [1/ms]
p_2 = 0.1 [1/mV]
p_3 = 4e-5 [1/ms]
p_4 = 0.05 [1/mV]
