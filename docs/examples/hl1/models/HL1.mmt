[[model]]
author: Charles Houston
desc: """
    Computer model of action potential of HL-1 myocytes.
    """
name: hl1

# Initial values
membrane.V = -67.0
ina.m = 0.01
ina.h = 0.99
ina.j = 0.99
ikr.xf = 0.01
ikr.xs = 0.01
ikr.r = 0.99
ikur.a = 0.001
ikur.i = 0.999
ito.x = 0.01
ito.y = 0.99
iha.y = 0.01
icat.b = 0.01
icat.g = 0.99
ical.d   = 0.01
ical.f   = 0.99
ical.fCa = 0.99

ryanodine_receptors.P_open = 0.012001999999999999
ca_conc_sr.Ca_SRuptake = 787.180644
ca_conc_sr.Ca_SRrelease = 776.6058415
ca_conc.Ca_i = 0.215237
na_conc.Na_i = 15558.639856999998
k_conc.K_i = 120499.57624000001


[environment]
time = 0 bind time
    in [ms]


[membrane]
Ca_o = 1800
    in [uM]
K_o = 4e3
    in [uM]
Na_o = 1.4e5
     in [uM]
Cm = 1
    in [uF/cm^2]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
T = 305
    in [K]
FoRT = F / (R * T)
    in [1/mV]

dot(V) = -(ical.i_CaL + incx.i_NCX + icab.i_Cab + ina.i_Na + inab.i_Nab + inak.i_NaK + ito.i_to + ik1.i_K1 + ikur.i_Kur + ikr.i_Kr + icat.i_CaT + iha.i_ha - i_stim) / Cm
    in [mV]
i_stim = 0.0
    in [pA/pF]

pi = 3.14159265358979323846
    in [1]
Acap = 1.38544e-5
    in [cm^2]
r_SR = 6        # radius of SR
    in [um]
r_SL = 10.5     # radius of SL
    in [um]
V_myo = (4 / 3 * pi * r_SL ^ 3 - 4 / 3 * pi * r_SR ^ 3) / 1000
    in [pL]


# Intracellular sodium concentration
[na_conc]
use membrane.V_myo as V_myo
use membrane.Acap as Acap
use membrane.Cm as Cm
use membrane.F as F
use membrane.FoRT as FoRT
use membrane.Na_o as Na_o

dot(Na_i) = -(ina.i_Na + inab.i_Nab + iha.i_haNa + 3 * incx.i_NCX + 3 * inak.i_NaK) * Acap * Cm / (V_myo * F * 1e-6)
    in [uM]
E_Na = 1 / FoRT * log(Na_o / Na_i)
    in [mV]


# Intracellular potassium concentration
[k_conc]
use membrane.V_myo as V_myo
use membrane.Acap as Acap
use membrane.Cm as Cm
use membrane.F as F
use membrane.FoRT as FoRT
use membrane.K_o as K_o

dot(K_i) = -(iha.i_haK + ito.i_to + ik1.i_K1 + ikr.i_Kr - 2 * inak.i_NaK + membrane.i_stim) * Acap * Cm / (V_myo * F * 1e-6)
    in [uM]
E_K = 1 / FoRT * log(K_o / K_i)
    in [mV]


# Fast sodium current
[ina]
use membrane.V as V
use na_conc.E_Na as E_Na

g_Na = 35
    in [mS/uF]
G_Na = g_Na * m ^ 3 * h * j
    in [mS/uF]
i_Na = G_Na * (V - E_Na)
    in [pA/pF]

dot(m) = (mss - m) / tau_m
    in [1]
mss = (1 + exp((Vhalf_m - V) / k_m)) ^ (-1)
    in [1]
tau_m = c_bm + c_am * exp(-1 * (Vmax_m - V)^2 / (sigma_m^2))
    in [ms]
Vhalf_m = -45
k_m = 6.5
c_bm = 0.02
c_am = 0.12
sigma_m = 25
Vmax_m = -40

dot(h) = (hss - h) / tau_h
    in [1]
hss = (1 + exp((Vhalf_h - V) / k_h)) ^ (-1)
    in [1]
tau_h = c_bh + c_ah * exp(-1 * (Vmax_h - V)^2 / (sigma_h^2))
    in [ms]
Vhalf_h = -76.1
k_h = -6.07
c_bh = 0.01
c_ah = 30.0
Vmax_h = -60
sigma_h = 25

dot(j) = (jss - j) / tau_j
    in [1]
jss = hss
    in [1]
tau_j = c_bj + c_aj * exp(-1 * (Vmax_j - V)^2 / (sigma_j^2))
    in [ms]
c_bj = 5.0
c_aj = 75.0
Vmax_j = -60
sigma_j = 25


# Rapid delayed rectifier potassium current
[ikr]
use membrane.V as V
use membrane.K_o as K_o
use k_conc.E_K as E_K

g_Kr = 0.5228
    in [mS/uF]
G_Kr = g_Kr * sqrt(K_o / 5400) * x * r # * rslow
    in [mS/uF]

i_Kr = G_Kr * (V - E_K)
    in [pA/pF]

x = A * xf + (1 - A) * xs
    in [1]
dot(xf) = (xss - xf) / tau_xf
    in [1]
dot(xs) = (xss - xs) / tau_xs
    in [1]
xss = (1 + exp((Vhalf_x - V) / k_x)) ^ (-1)
    in [1]
tau_xf = c_bxf + c_axf * exp(-1 * (Vmax_x - V)^2 / (sigma_x^2))
    in [ms]
tau_xs = c_bxs + c_axs * exp(-1 * (Vmax_x - V)^2 / (sigma_x^2))
    in [ms]
A = (1 + exp((Vhalf_A - V) / k_A)) ^ (-1)
    in [1]
    
Vhalf_x = -12.5
k_x = 10.0
c_bxf = 1
c_axf = 80
c_bxs = 50
c_axs = 600
sigma_x = 30
Vmax_x = -40
Vhalf_A = -25
k_A = -5

dot(r) = (rss - r) / tau_r
    in [1]
#r = rss
#    in [1]
rss = (1 + exp((Vhalf_r - V) / k_r)) ^ (-1)
    in [mV]
tau_r = c_br + c_ar * exp(-1 * (Vmax_r - V)^2 / (sigma_r^2))
    in [ms]

#dot(rslow) = (rss - rslow) / tau_rslow
#    in [1]
#tau_rslow = c_brslow + c_arslow * exp(-1 * (Vmax_r - V)^2 / (sigma_r^2))
#    in [ms]

Vhalf_r = -50.0
k_r = -10.0
c_br = 1
c_ar = 20
sigma_r = 20
Vmax_r = -30  

#c_brslow = 50
#c_arslow = 550


# Ultra-rapidly activating delayed rectifier potassium current
[ikur]
use membrane.V as V
use k_conc.E_K as E_K
    
g_Kur = 0.0975
    in [mS/uF]
G_Kur = g_Kur * a * i
    in [mS/uF]
i_Kur = G_Kur * (V - E_K)
    in [pA/pF]

dot(a) = (ass - a) / tau_a
    in [1]
ass = (1 + exp((Vhalf_a - V) / k_a)) ^ (-1)
    in [mV]
tau_a = c_ba + c_aa * exp(-1 * (Vmax_a - V)^2 / (sigma_a^2))
    in [ms]

Vhalf_a = -20
k_a = 7.7
c_ba = 2.058
c_aa = 0.493
sigma_a = 4
Vmax_a = -80

dot(i) = (iss - i) / tau_i
    in [1]
iss = (1 + exp((Vhalf_i - V) / k_i)) ^ (-1)
    in [mV]
tau_i = c_bi + c_ai * exp(-1 * (Vmax_i - V)^2 / (sigma_i^2))
    in [ms]

Vhalf_i = -45.2
k_i = -5.7
c_bi = 1200
c_ai = 1700
sigma_i = 5
Vmax_i = 20


# Transient outward potassium current
[ito]
use membrane.V as V
use k_conc.E_K as E_K

g_to = 0.12375
     in [nS/pF]
G_to = g_to * x * y
    in [nS/pF]
i_to = G_to * (V - E_K)
    in [pA/pF]

dot(x) = (xss - x) / tau_x
    in [1]
xss = (1 + exp((Vhalf_x - V) / k_x)) ^ (-1)
    in [1]
tau_x = c_bx + c_ax * exp(-1 * (Vmax_x - V)^2 / (sigma_x^2))
    in [ms]
Vhalf_x = -30
k_x = 11
c_bx = 1.5
c_ax = 3.5
Vmax_x = -10
sigma_x = 5.5

dot(y) = (yss - y) / tau_y
    in [1]
yss = (1 + exp((Vhalf_y - V) / k_y)) ^ (-1)
    in [1]
tau_y = c_by + c_ay * exp(-1 * (Vmax_y - V)^2 / (sigma_y^2))
    in [ms]
Vhalf_y = -40.5
k_y = -11.5
c_by = 21.21
c_ay = 38.4525
sigma_y = 4
Vmax_y = -52


# Hyperpolarisation activated nonselective cation current
[iha]
use membrane.V as V
use na_conc.E_Na as E_Na
use k_conc.E_K as E_K

g_ha = 0.021
    in [mS/uF]
G_ha = g_ha * y
    in [mS/uF]
i_haNa = G_ha * k_i_haNa * (V - E_Na)
    in [pA/pF]
i_haK = G_ha * (1-k_i_haNa) * (V - E_K)
    in [pA/pF]
i_ha = i_haNa + i_haK
    in [pA/pF]

k_i_haNa = 0.2
    in [1]

dot(y) = (yss - y) / tau_y
    in [1]
yss = (1 + exp((Vhalf_y - V) / k_y)) ^ (-1)
    in [1]
tau_y = c_by + c_ay * exp(-1 * (Vmax_y - V)^2 / (sigma_y^2))
    in [ms]
Vhalf_y = -78.65
k_y = -6.33
c_by = 1.0
c_ay = 5000
sigma_y = 50
Vmax_y = 30


# T-type calcium current
[icat]
use membrane.V as V
use ca_conc.E_Ca as E_Ca

g_CaT = 0.2
    in [mS/uF]
E_CaT = 33
    in [mV]
v_offset = 100.0
    in [mV]
G_CaT = g_CaT * b * g
    in [mS/uF]
i_CaT = G_CaT * (V - E_Ca + v_offset)
    in [pA/pF]

dot(b) = (bss - b) / tau_b
    in [1]
bss = (1 + exp((Vhalf_b - V) / k_b)) ^ (-1)
    in [mV]
tau_b = c_bb + c_ab * exp(-1 * (Vmax_b - V)^2 / (sigma_b^2))
    in [ms]

Vhalf_b = -40 
k_b = 5.40634
c_bb = 5
c_ab = 5.4
sigma_b = 15
Vmax_b = 50

dot(g) = (gss - g) / tau_g
    in [1]
gss = (1 + exp((Vhalf_g - V) / k_g)) ^ (-1)
    in [mV]
tau_g = c_bg + c_ag * exp(-1 * (Vmax_g - V)^2 / (sigma_g^2))
    in [ms]

Vhalf_g = -60
k_g = -2
c_bg = 10
c_ag = 100
sigma_g = 10
Vmax_g = -70


# L-type calcium current
[ical]
use membrane.V as V
use ca_conc.E_Ca as E_Ca
use ca_conc.Ca_subSL as Ca_subSL

i_CaL = G_CaL * (V - E_Ca + v_offset)
    in [pA/pF]
g_CaL = 0.5
    in [mS/uF]
G_CaL = g_CaL * d * f * fCa
    in [mS/uF]
v_offset = 60
    in [mV]

dot(d) = (dss - d) / tau_d
    in [1]
dss = (1 + exp((Vhalf_d - V) / k_d)) ^ (-1)
    in [1]
tau_d = c_bd + c_ad * exp(-1 * (Vmax_d - V)^2 / (sigma_d^2))
    in [ms]
Vhalf_d = -11.1
k_d = 7.2
c_bd = 0.25
c_ad = 1.4
sigma_d = 2
Vmax_d = -35

dot(f) = (fss - f) / tau_f
    in [1]
fss = (1 + exp((Vhalf_f - V) / k_f)) ^ (-1)
    in [1]
tau_f = c_bf + c_af * exp(-1 * (Vmax_d - V)^2 / (sigma_f^2))
    in [ms]
Vhalf_f = -23.3
k_f = -5.4
c_bf = 120
c_af = 1125
Vmax_f = -27
sigma_f = 15

dot(fCa) = (fCass - fCa) / tau_fCa
    in [1]
fCass = (1 + (Ca_subSL / ca_dep_fCa)) ^ (-1)
    in [1]
ca_dep_fCa = 0.35
    in [uM]
tau_fCa = 10
    in [ms]


# Inward rectifying potassium current
[ik1]
use membrane.V as V
use k_conc.E_K as E_K
use membrane.K_o as K_o

i_K1 = G_K1 * (K_o / (K_o + Km)) * (V - E_K) 
G_K1 = g_K1 * xss
    in [mS/uF]
g_K1 = 0.0515 [mS/uF]
    in [mS/uF]

xss = (1 + exp((Vhalf_x - V) / k_x)) ^ (-1)
    in [1]

Km = 210
    in [mM]
Vhalf_x = -80
k_x = 30


# Na+/Ca2+ exchanger current - Korhonen et al. (2009)
[incx]
use membrane.Na_o as Na_o
use membrane.Ca_o as Ca_o
use ca_conc.Ca_subSL as Ca_subSL
use membrane.V as V
use na_conc.Na_i as Na_i

k_NCX = 2.268 # scaling factor
    in [pA/pF/uM^4]
d_NCX = 1e-16 # denominator constant
    in [uM^-4]
gamma = 0.5 # energy barrier
    in [1]

i_NCX = k_NCX * 1e-16 * (Na_i ^ 3 * Ca_o * exp(0.03743 * gamma * V) - Na_o ^ 3 * Ca_subSL * exp(0.03743 * (gamma - 1) * V)) / (1 + d_NCX * (Na_o ^ 3 * Ca_subSL + Na_i ^ 3 * Ca_o))


# Na+/K+ ATPase - Korhonen et al. (2009)
[inak]
use membrane.V as V
use membrane.FoRT as FoRT
use na_conc.Na_i as Na_i
use membrane.K_o as K_o
use membrane.Na_o as Na_o

i_NaK_max = 2.7         # maximum current amplitude
    in [pA/pF]
n_NaK = 3.2             # hill coefficient for Na+
    in [1]
Km_Nai = 18600          # Na+ half saturation constant
    in [uM]
Km_Ko = 1500            # K+ half saturation constant
    in [uM]

f_NaK = (1 + 0.1245 * exp(-0.1 * V * FoRT) + 0.0365 * sigma * exp(-V * FoRT)) ^ (-1)
    in [1]
sigma = 1 / 7 * (exp(Na_o / 67300 [uM]) - 1)
    in [1]

i_NaK = i_NaK_max * f_NaK * (1 + (Km_Nai / Na_i) ^ n_NaK) ^ (-1) * (1 + (Km_Ko / K_o)) ^ (-1)
    in [pA/pF]


# Calcium background current
[icab]
use membrane.V as V
use ca_conc.E_Ca as E_Ca

g_Cab = 0.0008
  in [mS/uF]
i_Cab = g_Cab * (V - E_Ca)
  in [pA/pF]

# Background sodium current
[inab]
use membrane.V as V
use na_conc.E_Na as E_Na

g_Nab = 0.0026
    in [mS/uF]
i_Nab = g_Nab * (V - E_Na)
    in [pA/pF]


# Sarcoplasmic reticulum - Korhonen et al. (2009)
[ryanodine_receptors]
k_RyR = 0.01        # scaling factor for J_RyR
    in [1/ms]
k_open = 1          # rate constant for RyR opening
    in [1/ms]
k_close = 0.16      # rate constant for RyR closing
    in  [1/ms]
j_1 = 3.51
    in [1]
j_2 = 530
    in [uM]
j_3 = 200
    in [uM]
j_4 = 0.25
    in [1]

J_RyR = k_RyR * P_open * (ca_conc_sr.Ca_SRrelease - ca_conc.Ca_subSR)
    in [uM/ms]
J_RyRCaffeine = 5.5e-4 * (ca_conc_sr.Ca_SRrelease - ca_conc.Ca_subSR)
    in [uM/ms]
K_mRyR = j_1 * (1 + exp((ca_conc_sr.Ca_SRrelease - j_2) / j_3)) ^ (-1) + j_4
    in [uM]
P_closed = 1 - P_open
    in [1]

dot(P_open) = P_closed * k_open * ca_conc.Ca_subSR ^ 4 / (K_mRyR ^ 4 + ca_conc.Ca_subSR ^ 4) - k_close * P_open
    in [1]

[serca]
V_max = 0.9996      # maximum SERCA calcium_fluxes
    in [uM/ms]
K_mf = 0.5          # half-saturation for forward SERCA
    in [uM]
K_mr = 7000 * K_mf  # half-saturation for reverse SERCA
    in [uM]
H = 2               # hill coefficient for SERCA
    in [1]

J_SERCA = (V_max * (ca_conc.Ca_subSR / K_mf) ^ H - V_max * (ca_conc_sr.Ca_SRuptake / K_mr) ^ H) / (1 + (ca_conc.Ca_subSR / K_mf) ^ H + (ca_conc_sr.Ca_SRuptake / K_mr) ^ H)
    in [uM/ms]

[leak_flux]
k_leak = 5e-6       # SR leak rate constant
    in [1/ms]

J_leak = k_leak * (ca_conc_sr.Ca_SRuptake - ca_conc.Ca_subSR)
    in [uM/ms]

[ca_diffusion]
tau_tr = 100        # time constant for J_tr
    in [ms]

J_tr = (ca_conc_sr.Ca_SRuptake - ca_conc_sr.Ca_SRrelease) / tau_tr
    in [uM/ms]

[ca_conc_sr]
CSQN_tot = 24750        # total calsequestrin concentration
    in [uM]
K_mCSQN = 800           # half-saturation for calsequestrin
    in [uM]
V_SRuptake = 0.08127    # volume of SRuptake compartment
    in [pL]
V_SRrelease = 0.009030  # volume of SRrelease compartment
    in [pL]

beta_SRrelease = (1 + CSQN_tot * K_mCSQN / ((Ca_SRrelease + K_mCSQN) ^ 2)) ^ (-1)

dot(Ca_SRuptake) = (serca.J_SERCA - leak_flux.J_leak - ca_diffusion.J_tr) / V_SRuptake
    in [uM]
dot(Ca_SRrelease) = beta_SRrelease * (-ryanodine_receptors.J_RyR + ca_diffusion.J_tr) / V_SRrelease
    in [uM]

Ca_SR = (Ca_SRuptake * V_SRuptake + Ca_SRrelease * V_SRrelease) / (V_SRuptake + V_SRrelease)
    in [uM]


# Intracellular calcium handling
# Simplified from Korhonen et al (2009)
[ca_conc]
use membrane.pi as pi
use membrane.FoRT as FoRT
use membrane.Ca_o as Ca_o
use membrane.Acap as Acap
use membrane.Cm as Cm
use membrane.F as F
use membrane.V_myo as V_myo
use membrane.r_SR as r_SR
use membrane.r_SL as r_SL

K_mTRPN = 0.5   # half-saturation for troponin
    in [uM]
TRPN_tot = 35   # total troponin concentration
    in [uM]
K_mCMDN = 2.38  # half-saturation for calmodulin
    in [uM]
CMDN_tot = 50   # total calmodulin concentration
    in [uM]

J_CaSR = ryanodine_receptors.J_RyR - serca.J_SERCA + leak_flux.J_leak
    in [uM/ms]
J_CaSL = (2 * incx.i_NCX - ical.i_CaL - icat.i_CaT - icab.i_Cab) * Acap * Cm / (2 * F * 1e-6)
    in [uM/ms]

beta = (1 + TRPN_tot * K_mTRPN / ((Ca_i + K_mTRPN) ^ 2) + CMDN_tot * K_mCMDN / ((Ca_i + K_mCMDN) ^ 2)) ^ (-1)
    in [1]
dot(Ca_i) = beta * (J_CaSR + J_CaSL) / V_myo
    in [uM]

Ca_subSR = Ca_i
    in [uM]
Ca_subSL = Ca_i
    in [uM]
E_Ca = 1 / (2 * FoRT) * log(Ca_o / Ca_i)
    in [mV]
