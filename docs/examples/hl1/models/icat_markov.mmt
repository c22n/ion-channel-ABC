[[model]]
name: T-type Ca2+ channel model
author: Charles Houston
desc: """
      Simple model for ICaT.
      """
# Initial values
membrane.V = -67
icat.a = 0.0
icat.r = 1.0

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -icat.i_CaT
    in [mV]

[extra]
Ca_o = 1.8e3 [uM]

[phys]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
T = 296 [K]
FoRT = F / (R * T)

[calcium]
use phys.FoRT as FoRT
use extra.Ca_o as Ca_o
    
Ca_i = 0.2 [uM]
E_Ca = 1 / (2 * FoRT) * log(Ca_o / Ca_i)
    in [mV]

[icat]
use membrane.V as V

g_CaT = 1.0 [mS/uF]
E_CaT = 45 [mV]
g = a * r
    in [1]
i_CaT = g_CaT * g * (V - E_CaT)
    in [pA/pF]

dot(a) = (a_ss - a) / tau_a
    in [1]
tau_a = 1 / (k_1 + k_2)
    in [ms]
a_ss = k_1 * tau_a
    in [1]

dot(r) = (r_ss - r) / tau_r
    in [1]
tau_r = 1 / (k_3 + k_4)
    in [ms]
r_ss = k_4 * tau_r
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]
k_3 = p_5*exp(p_6 * V)
    in [1/ms]
k_4 = p_7*exp(-p_8 * V)
    in [1/ms]

p_1 = 1e-4 [1/ms]
p_2 = 0.1 [1/mV]
p_3 = 4e-5 [1/ms]
p_4 = 0.05 [1/mV]
p_5 = 5e-2 [1/ms]
p_6 = 0.01 [1/mV]
p_7 = 6e-3 [1/ms]
p_8 = 0.05 [1/mV]
