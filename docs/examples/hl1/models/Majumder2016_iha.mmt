[[model]]
name: Majumder 2016 hyperpolarisation activated non-selective cation current
author: Charles Houston

# Initial values
membrane.V = -67
iha.y = 0.01

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -iha.i_ha
    in [mV]

[phys]
T = 310 [K]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
FoRT = F/(R*T)

[extra]
K_o = 25000 [uM]
Na_o = 140000 [uM]

[potassium]
use phys.FoRT as FoRT
use extra.K_o as K_o

K_i = 120000 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[sodium]
use phys.FoRT as FoRT
use extra.Na_o as Na_o

Na_i = 5400 [uM]
E_Na = 1/FoRT * log(Na_o/Na_i)
    in [mV]


[iha]
use membrane.V as V
use sodium.E_Na as E_Na
use potassium.E_K as E_K

g_ha = 0.021 [mS/uF]

g = y
    in [1]
i_haNa = g_ha * g * k_i_haNa * (V-E_Na)
    in [pA/pF]
i_haK = g_ha * g * (1-k_i_haNa) * (V-E_K)
    in [pA/pF]

k_i_haNa = 0.2
    in [1]

i_ha = i_haNa + i_haK
    in [pA/pF]

y_ss = 1 / (1 + exp((V + k_yss1) / k_yss2))
    in [1]
k_yss1 = 78.65 [mV]
k_yss2 = 6.33 [mV]

tau_y = 1000 * k_ytau1 / (k_ytau2 * exp((V + k_ytau3) / k_ytau4) + k_ytau5 * exp((V + k_ytau6) / -k_ytau7))
    in [ms]
k_ytau1 = 1 [ms]
k_ytau2 = 0.11885
k_ytau3 = 75 [mV]
k_ytau4 = 28.37 [mV]
k_ytau5 = 0.56236
k_ytau6 = 75 [mV]
k_ytau7 = 14.19 [mV]

dot(y) = (y_ss - y) / tau_y
    in [1]
