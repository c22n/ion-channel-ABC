[[model]]
name: Takeuchi 2013 transient outward K Current
author: Charles Houston

# Initial values
membrane.V = -67.0
ito.xto = 4.05157399999999963e-3
ito.yto = 0.9945511

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ito.i_to
    in [mV]

[phys]
T = 310 [K]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
FoRT = F/(R*T)

[extra]
K_o = 5.4e3 [uM]

[potassium]
use phys.FoRT as FoRT
use extra.K_o as K_o

K_i = 143720 [uM]
E_K = 1/FoRT * log(K_o/K_i)
    in [mV]

[ito]
use membrane.V as V
use potassium.E_K as E_K

g_to = 0.12375 [nS/pF]
     in [nS/pF]

xto_ss = 1 / (1 + exp(-(V - k_xss1) / k_xss2))
    in [1]
tau_xto = k_xtau1 + k_xtau2 * exp(-((V) / k_xtau3) ^ 2)
    in [ms]
yto_ss = 1 / (1 + exp((V + k_yss1) / k_yss2))
    in [1]
tau_yto = k_ytau1 + k_ytau2 * exp(-((V + k_ytau3) / k_ytau4) ^ 2)
    in [ms]
dot(xto) = (xto_ss - xto) / tau_xto
    in [1]
dot(yto) = (yto_ss - yto) / tau_yto
    in [1]
k_xss1 = 1 [mV]
    in [mV]
k_xss2 = 11 [mV]
    in [mV]
k_xtau1 = 1.5 [ms]
    in [ms]
k_xtau2 = 3.5 [ms]
    in [ms]
k_xtau3 = 30 [mV]
    in [mV]
k_yss1 = 40.5 [mV]
    in [mV]
k_yss2 = 11.5 [mV]
    in [mV]
k_ytau1 = 21.21 [ms]
    in [ms]
k_ytau2 = 38.4525 [ms]
    in [ms]
k_ytau3 = 52.45 [mV]
    in [mV]
k_ytau4 = 15.8827 [mV]
    in [mV]

G_to = g_to * xto * yto
    in [nS/pF]
i_to = G_to * (V - E_K)
    in [pA/pF]
