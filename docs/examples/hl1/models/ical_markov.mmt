[[model]]
name: L-type Ca2+ channel model
author: Charles Houston
desc: """
      Simple model for ICaL.
      """
# Initial values
membrane.V = -67
ical.d = 0.0
ical.f = 1.
ical.fCa = 1.

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ical.i_CaL
    in [mV]

[extra]
Ca_o = 1800 [uM]

[calcium]
Ca_i = 0.2 [uM]
Ca_subSL = Ca_i

[phys]
F = 96.5 [C/mmol]
R = 8.314 [J/mol/K]
T = 296 [K]
FoRT = F / (R * T)

[ical]
use membrane.V as V
use extra.Ca_o as Ca_o
use calcium.Ca_subSL as Ca_subSL

g_CaL = 0.4 [mS/uF]
g = d*f*fCa
E_CaL = 45 [mV]
i_CaL = g_CaL * g * (V - E_CaL)

dot(d) = (d_ss - d) / tau_d
    in [1]
tau_d = 1 / (k_1 + k_2)
    in [ms]
d_ss = k_1 * tau_d
    in [1]

dot(f) = (f_ss - f) / tau_f
    in [1]
tau_f = 1 / (k_3 + k_4)
    in [ms]
f_ss = k_4 * tau_f
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]
k_3 = p_5*exp(p_6 * V)
    in [1/ms]
k_4 = p_7*exp(-p_8 * V)
    in [1/ms]

p_1 = 1e-4 [1/ms]
p_2 = 0.1 [1/mV]
p_3 = 4e-5 [1/ms]
p_4 = 0.05 [1/mV]
p_5 = 5e-2 [1/ms]
p_6 = 0.01 [1/mV]
p_7 = 6e-3 [1/ms]
p_8 = 0.05 [1/mV]

# Calcium-dependent inactivation [Kurata2002]
dot(fCa) = (fCass - fCa)/tau_fCa
    in [1]
Km_fCa = 0.35 [uM]    : ICaL calcium dissociation constant
a_fCa = 0.035 [ms^-1] : ICaL calcium dissociation rate 

fCass = Km_fCa / (Km_fCa + calcium.Ca_subSL)
tau_fCa = fCass/a_fCa
