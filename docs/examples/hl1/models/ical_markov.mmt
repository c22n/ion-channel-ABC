[[model]]
name: L-type Ca2+ channel model
author: Charles Houston
desc: """
      Markov model for the L-type calcium channel.
      """

# Initial values
membrane.V = -67
ical.d = 0.01
ical.f = 0.99
ical.fCa = 0.99

[environment]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ical.i_CaL
    in [mV]
Ca_o = 1800
    in [uM]
Ca_i = 0.2
    in [uM]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
T = 295
    in [K]
FoRT = F / (R * T)
    in [1/mV]

[ical]
use membrane.V as V
use membrane.Ca_o as Ca_o
use membrane.Ca_i as Ca_i
use membrane.FoRT as FoRT
use membrane.F as F

P_CaL = 1e-3
    in [m/s]
g = d * f * fCa
    in [1]
driving = if(V != 0,
    4*V*FoRT*F*(Ca_i-gamma_Ca_o*Ca_o*exp(-2*V*FoRT))/(1-exp(-2*V*FoRT)),
    2*F*(Ca_i-gamma_Ca_o*Ca_o))
    in [pA/pF]
i_CaL = P_CaL * g * driving
    in [pA/pF]
gamma_Ca_o = 1.0
    in [1]

dot(d) = (d_ss - d) / tau_d
    in [1]
tau_d = 1 / (k_1 + k_2)
    in [ms]
d_ss = k_1 * tau_d
    in [1]

dot(f) = (f_ss - f) / tau_f
    in [1]
tau_f = 1 / (k_3 + k_4)
    in [ms]
f_ss = k_4 * tau_f
    in [1]

k_1 = p_1*exp(p_2 * V)
    in [1/ms]
k_2 = p_3*exp(-p_4 * V)
    in [1/ms]
k_3 = p_5*exp(p_6 * V)
    in [1/ms]
k_4 = p_7*exp(-p_8 * V)
    in [1/ms]

p_1 = 1e-4
    in [1/ms]
p_2 = 0.1 
    in [1/mV]
p_3 = 4e-5
    in [1/ms]
p_4 = 0.05
    in [1/mV]
p_5 = 5e-2
    in [1/ms]
p_6 = 0.01
    in [1/mV]
p_7 = 6e-3
    in [1/ms]
p_8 = 0.05
    in [1/mV]

# Calcium-dependent inactivation from Korhonen 2009
dot(fCa) = k*((fCass - fCa)/tau_fCa)
    in [1]
k = if(fCass > fCa, if(V > -60, 0, 1), 1)
    in [1]
alphafCa = (1 + (Ca_i/0.4875)^8)^(-1)
    in [1]
betafCa = 0.1 * (1 + exp((Ca_i-0.5)/0.1))^(-1)
    in [1]
gammafCa = 0.2 * (1 + exp((Ca_i-0.75)/0.8))^(-1)
    in [1]
fCass = (alphafCa+betafCa+gammafCa+0.23)/k_fCass
    in [1]
k_fCass = 1.46
    in [1]
tau_fCa = 2
    in [ms]
