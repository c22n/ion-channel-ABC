[[model]]
name: Korhonen 2009 L-type calcium channel
author: Charles Houston
desc: """
      The Korhonen 2009 model for the L-type calcium current.
      """
ref: """
     T Korhonen et al. (2009) Model of Excitation-Contraction Coupling of Rat
     Neonatal Ventricular Myocytes.
     """
# Initial values
membrane.V = -67.0
ical.d = 1.40562700000000000e-3
ical.f = 0.99
ical.fCa = 0.99

[engine]
time = 0 bind time
    in [ms]

[membrane]
dot(V) = -ical.i_CaL
    in [mV]

[phys]
T = 296
    in [K]
F = 96.5
    in [C/mmol]
R = 8.314
    in [J/mol/K]
FoRT = F / (R * T)

[extra]
Ca_o = 1800
    in [uM]

[calcium]
Ca_i = 0.115001
    in [uM]

[ical]
use membrane.V as V
use phys.FoRT as FoRT
use phys.F as F
use extra.Ca_o as Ca_o
use calcium.Ca_i as Ca_i

i_CaL = g_CaL * g * 4 * V * F * FoRT * (Ca_i * exp(2 * V * FoRT) - 0.341 * Ca_o) / (exp(2 * V * FoRT) - 1)
    in [pA/pF]
g_CaL = 0.000063
    in [dm^3/F/ms]
g = d * f * fCa
    in [1]

dot(d) = (d_ss - d) / tau_d
    in [1]
d_ss = (1 + exp((p1 - V) / p2)) ^ (-1)
    in [1]
alpha_d = 1.4 * (1 + exp((p3 - V) / p4)) ^ (-1) + 0.25
    in [ms]
beta_d = 1.4 * (1 + exp((V + p5) / p6)) ^ (-1)
    in [ms]
gamma_d = (1 + exp((p7 - V) / p8)) ^ (-1)
    in [ms]
tau_d = alpha_d * beta_d + gamma_d
    in [ms]
p1 = -11.1
p2 = 7.2
p3 = -35
p4 = 13
p5 = 5
p6 = 5
p7 = 50
p8 = 20


dot(f) = (f_ss - f) / tau_f
    in [1]
f_ss = (1 + exp((q1 + V) / q2)) ^ (-1)
    in [1]
tau_f = q3 * exp(-(V + q4) ^ 2 / q5) + q6 * (1 + exp((q7 - V) / q8)) ^ (-1) + q9
    in [ms]
q1 = 23.3
q2 = 5.4
q3 = 1125
q4 = 27
q5 = 240
q6 = 165
q7 = 25
q8 = 10
q9 = 120

dot(fCa) = k * (fCa_ss - fCa) / tau_fCa
    in [1]
alpha_fCa = (1 + (Ca_i / r1) ^ 8) ^ (-1)
    in [1]
beta_fCa = r2 * (1 + exp((Ca_i - r3) / r4)) ^ (-1)
    in [1]
gamma_fCa = r5 * (1 + exp((Ca_i - r6) / r7)) ^ (-1)
    in [1]
fCa_ss = (alpha_fCa + beta_fCa + gamma_fCa + r8) / r9
    in [1]
tau_fCa = 10
    in [ms]
k = piecewise(fCa_ss > fCa and V > -60, 0, 1)
    in [1]
r1 = 0.4875
r2 = 0.1
r3 = 0.5
r4 = 0.1
r5 = 0.2
r6 = 0.75
r7 = 0.8
r8 = 0.23
r9 = 1.46
